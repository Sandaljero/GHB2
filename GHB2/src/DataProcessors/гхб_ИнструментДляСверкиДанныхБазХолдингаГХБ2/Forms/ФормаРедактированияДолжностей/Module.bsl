#Область СобытияФормыЭлементовФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	БазаХолдингаСсылка = Параметры.БазаХолдинга;
	ЗаполнитьДанныеДляСоздания();
	ЗаполнитьДанныеДляРедактирования();
	ЗаполнитьДанныеДляУдаления();
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура УстановитьФлагиНовые(Команда)
	
	Для каждого СтрокаДанные Из ДанныеНовые Цикл
		СтрокаДанные.ДН = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлагиНовые(Команда)
	
	Для каждого СтрокаДанные Из ДанныеНовые Цикл
		СтрокаДанные.ДН = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлагиРедактирование(Команда)
	
	Для каждого СтрокаДанные Из ДанныеРедактирование Цикл
		СтрокаДанные.ДН = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлагиРедактирование(Команда)
	
	Для каждого СтрокаДанные Из ДанныеРедактирование Цикл
		СтрокаДанные.ДН = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлагиУдаление(Команда)
	
	Для каждого СтрокаДанные Из ДанныеУдаление Цикл
		СтрокаДанные.ДН = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлагиУдаление(Команда)
	
	Для каждого СтрокаДанные Из ДанныеУдаление Цикл
		СтрокаДанные.ДН = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	РедактироватьСервер();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДолжности(Команда)
	СоздатьДолжностиСервер();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьДолжности(Команда)
	УстановитьПометкуУдаленияСервер();
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура РедактироватьСервер()

	мНайденное = ДанныеРедактирование.НайтиСтроки(Новый Структура("ДН,Обработано", Истина, Ложь));
	
	Для каждого СтрокаНайденное Из мНайденное Цикл
		РедактироватьДолжность(СтрокаНайденное);
	КонецЦикла;

КонецПроцедуры // РедактироватьСервер()

&НаСервере
Процедура РедактироватьДолжность(ДанныеДляРедактирования)
	
	спрОбъект = ДанныеДляРедактирования.ДолжностьОрганизации.ПолучитьОбъект();
	
	Если спрОбъект.Наименование <> ДанныеДляРедактирования.Наименование И ЗначениеЗаполнено(ДанныеДляРедактирования.Наименование) Тогда
		спрОбъект.Наименование = ДанныеДляРедактирования.Наименование;
	КонецЕсли;
	
	Если спрОбъект.АнглийскоеНаименование <> ДанныеДляРедактирования.АнглийскоеНаименование Тогда
		спрОбъект.АнглийскоеНаименование = ДанныеДляРедактирования.АнглийскоеНаименование;
	КонецЕсли;
	
	Если спрОбъект.ПометкаУдаления <> ДанныеДляРедактирования.ПометкаУдаления Тогда
		спрОбъект.ПометкаУдаления = ДанныеДляРедактирования.ПометкаУдаления;
	КонецЕсли;
	
	Попытка
		
		спрОбъект.ДополнительныеСвойства.Вставить("ПометкаПришлаОбменом", Истина);
		спрОбъект.Записать();
		ДанныеДляРедактирования.Обработано = Истина;
		
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;

КонецПроцедуры // РедактироватьДолжность()

&НаСервере
Процедура СоздатьДолжностиСервер()

	мНайденное = ДанныеНовые.НайтиСтроки(Новый Структура("ДН,Обработано", Истина, Ложь));
	РодительБазаХолдинга = получитьГруппуПоОрганизацииВСправочникеДолжностиБазХолдинга();
	
	Если Не ЗначениеЗаполнено(РодительБазаХолдинга) Тогда
	
		ОбщегоНазначения.СообщитьПользователю("Не найдена папка(Родитель) по имени Базы холдинга");
		Возврат;
	
	КонецЕсли;
	
	Для каждого СтрокаНайденное Из мНайденное Цикл
		СоздатьДолжность(СтрокаНайденное, РодительБазаХолдинга);
	КонецЦикла;

КонецПроцедуры // СоздатьДолжностиСервер()

Функция получитьГруппуПоОрганизацииВСправочникеДолжностиБазХолдинга()

	Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БазаХолдингаСсылка, "Наименование");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	гхб_ДолжностиОрганизацийБазХолдинга.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.гхб_ДолжностиОрганизацийБазХолдинга КАК гхб_ДолжностиОрганизацийБазХолдинга
	|ГДЕ
	|	гхб_ДолжностиОрганизацийБазХолдинга.ЭтоГруппа
	|	И гхб_ДолжностиОрганизацийБазХолдинга.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции // ПроверитьСоздатьГруппуПоОрганизацииВСправочникеФизЛицаБазХолдинга()

Процедура СоздатьДолжность(ДанныеДляСоздания, РодительБазаХолдинга)

	спрОбъект = Справочники.гхб_ДолжностиОрганизацийБазХолдинга.СоздатьЭлемент();
	спрОбъект.ДополнительныеСвойства.Вставить("ПометкаПришлаОбменом", Истина);
	спрОбъект.Родитель = РодительБазаХолдинга;
	спрОбъект.Наименование = ДанныеДляСоздания.Наименование;
	спрОбъект.АнглийскоеНаименование = ДанныеДляСоздания.АнглийскоеНаименование;
	спрОбъект.GUIDБазыХолдинга = ДанныеДляСоздания.guid;
	спрОбъект.БазаХолдинга = БазаХолдингаСсылка;
	
	Попытка
		
		спрОбъект.Записать();
		ОбщегоНазначения.СообщитьПользователю("Создана должность " + спрОбъект.Ссылка);
		ДанныеДляСоздания.Обработано = Истина;
		
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкуУдаленияСервер()

	мНайденное = ДанныеУдаление.НайтиСтроки(Новый Структура("ДН,Обработано", Истина, Ложь));
	
	Для каждого СтрокаНайденное Из мНайденное Цикл
	
		спрОбъект = СтрокаНайденное.ДолжностьОрганизации.ПолучитьОбъект();
		спрОбъект.ДополнительныеСвойства.Вставить("ПометкаПришлаОбменом", Истина);
		спрОбъект.ПометкаУдаления = Истина;
		спрОбъект.НетСоответствияВУчетныхБазах = Истина;
		
		Попытка
		
			спрОбъект.Записать();
			СтрокаНайденное.Обработано = Истина;
		
		Исключение
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
	
	КонецЦикла;

КонецПроцедуры // УстановитьПометкуУдаленияСервер()

&НаСервере
Процедура ЗаполнитьДанныеДляСоздания()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("таб", Параметры.тзДанные);
	Запрос.УстановитьПараметр("БазаХолдинга", БазаХолдингаСсылка);
	Запрос.Текст = ПолучитьТекстЗапросаНетДанныхГХБ();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		новСтр = ДанныеНовые.Добавить();
		ЗаполнитьЗначенияСвойств(новСтр, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьДанныеДляСоздания()

&НаСервере
Процедура ЗаполнитьДанныеДляРедактирования()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("таб", Параметры.тзДанные);
	Запрос.УстановитьПараметр("БазаХолдинга", БазаХолдингаСсылка);
	Запрос.Текст = ПолучитьТекстЗапросаРазличиеДанных();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Отличия = "";
		
		новСтр = ДанныеРедактирование.Добавить();
		ЗаполнитьЗначенияСвойств(новСтр, Выборка);
		
		Отличия = "";
		
		Если Выборка.Наименование <> Выборка.НаименованиеБХ Тогда
			Отличия = Отличия + "Наименование, " + Символы.ПС;
		КонецЕсли;
		
		Если Выборка.АнглийскоеНаименование <> Выборка.АнглийскоеНаименованиеБХ Тогда
			Отличия = Отличия + "Английское наименование, " + Символы.ПС;
		КонецЕсли;
		
		Если Выборка.ПометкаУдаленияБХ <> Выборка.ПометкаУдаления Тогда
			Отличия = Отличия + "Пометка удаления " + Символы.ПС;
		КонецЕсли;
		
		новСтр.Отличия = Отличия;
		
	КонецЦикла;

КонецПроцедуры // ЗаполнитьДанныеДляРедактирования()

&НаСервере
Процедура ЗаполнитьДанныеДляУдаления()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("таб", Параметры.тзДанные);
	Запрос.УстановитьПараметр("БазаХолдинга", БазаХолдингаСсылка);
	Запрос.Текст = ПолучитьТекстЗапросаНетДанныхРИБ();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		новСтр = ДанныеУдаление.Добавить();
		ЗаполнитьЗначенияСвойств(новСтр, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьДанныеДляУдаления()

&НаСервере
Функция ПолучитьТекстЗапросаРазличиеДанных()

	Текст = 
	"ВЫБРАТЬ
	|	таб.Наименование КАК Наименование,
	|	таб.АнглийскоеНаименование КАК АнглийскоеНаименование,
	|	таб.ПометкаУдаления КАК ПометкаУдаления,
	|	таб.guid КАК guid
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	&таб КАК таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Наименование КАК Наименование,
	|	втДанные.guid КАК guid,
	|	втДанные.ПометкаУдаления КАК ПометкаУдаления,
	|	гхб_ДолжностиОрганизацийБазХолдинга.Ссылка КАК ДолжностьОрганизации,
	|	гхб_ДолжностиОрганизацийБазХолдинга.ПометкаУдаления КАК ПометкаУдаленияБаза,
	|	втДанные.АнглийскоеНаименование КАК АнглийскоеНаименование,
	|	гхб_ДолжностиОрганизацийБазХолдинга.Наименование КАК НаименованиеБХ,
	|	гхб_ДолжностиОрганизацийБазХолдинга.GUIDБазыХолдинга КАК guidБХ,
	|	гхб_ДолжностиОрганизацийБазХолдинга.АнглийскоеНаименование КАК АнглийскоеНаименованиеБХ,
	|	гхб_ДолжностиОрганизацийБазХолдинга.ПометкаУдаления КАК ПометкаУдаленияБХ
	|ИЗ
	|	втДанные КАК втДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гхб_ДолжностиОрганизацийБазХолдинга КАК гхб_ДолжностиОрганизацийБазХолдинга
	|		ПО втДанные.guid = гхб_ДолжностиОрганизацийБазХолдинга.GUIDБазыХолдинга
	|			И (гхб_ДолжностиОрганизацийБазХолдинга.БазаХолдинга = &БазаХолдинга)
	|ГДЕ
	|	(втДанные.Наименование <> гхб_ДолжностиОрганизацийБазХолдинга.Наименование
	|			ИЛИ втДанные.АнглийскоеНаименование <> гхб_ДолжностиОрганизацийБазХолдинга.АнглийскоеНаименование
	|			ИЛИ втДанные.ПометкаУдаления <> гхб_ДолжностиОрганизацийБазХолдинга.ПометкаУдаления)";
	
	Возврат Текст;

КонецФункции // ПолучитьТекстЗапросаРазличиеДанных()

&НаСервере
Функция ПолучитьТекстЗапросаНетДанныхГХБ()

	Текст = 
	"ВЫБРАТЬ
	|	таб.Наименование КАК Наименование,
	|	таб.АнглийскоеНаименование КАК АнглийскоеНаименование,
	|	таб.ПометкаУдаления КАК ПометкаУдаления,
	|	таб.guid КАК guid
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	&таб КАК таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Наименование КАК Наименование,
	|	втДанные.guid КАК guid,
	|	втДанные.ПометкаУдаления КАК ПометкаУдаления,
	|	гхб_ДолжностиОрганизацийБазХолдинга.Ссылка КАК Должность,
	|	ЛОЖЬ КАК ПометкаУдаленияБаза,
	|	втДанные.АнглийскоеНаименование КАК АнглийскоеНаименование
	|ИЗ
	|	втДанные КАК втДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_ДолжностиОрганизацийБазХолдинга КАК гхб_ДолжностиОрганизацийБазХолдинга
	|		ПО втДанные.guid = гхб_ДолжностиОрганизацийБазХолдинга.GUIDБазыХолдинга
	|			И (гхб_ДолжностиОрганизацийБазХолдинга.БазаХолдинга = &БазаХолдинга)
	|ГДЕ
	|	гхб_ДолжностиОрганизацийБазХолдинга.Ссылка ЕСТЬ NULL";
	
	Возврат Текст;

КонецФункции // ПолучитьТекстЗапросаНетДанныхГХБ()

&НаСервере
Функция ПолучитьТекстЗапросаНетДанныхРИБ()

	Текст = 
	"ВЫБРАТЬ
	|	таб.Наименование КАК Наименование,
	|	таб.АнглийскоеНаименование КАК АнглийскоеНаименование,
	|	таб.ПометкаУдаления КАК ПометкаУдаления,
	|	таб.guid КАК guid
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	&таб КАК таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	гхб_ДолжностиОрганизацийБазХолдинга.Наименование КАК Наименование,
	|	гхб_ДолжностиОрганизацийБазХолдинга.GUIDБазыХолдинга КАК guid,
	|	гхб_ДолжностиОрганизацийБазХолдинга.ПометкаУдаления КАК ПометкаУдаления,
	|	гхб_ДолжностиОрганизацийБазХолдинга.Ссылка КАК ДолжностьОрганизации,
	|	гхб_ДолжностиОрганизацийБазХолдинга.ПометкаУдаления КАК ПометкаУдаленияБаза,
	|	гхб_ДолжностиОрганизацийБазХолдинга.АнглийскоеНаименование КАК АнглийскоеНаименование
	|ИЗ
	|	Справочник.гхб_ДолжностиОрганизацийБазХолдинга КАК гхб_ДолжностиОрганизацийБазХолдинга
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДанные КАК втДанные
	|		ПО гхб_ДолжностиОрганизацийБазХолдинга.GUIDБазыХолдинга = втДанные.guid
	|ГДЕ
	|	втДанные.Наименование ЕСТЬ NULL
	|	И гхб_ДолжностиОрганизацийБазХолдинга.БазаХолдинга = &БазаХолдинга";
	
	Возврат Текст;

КонецФункции // ПолучитьТекстЗапросаНетДанныхРИБ()

#КонецОбласти