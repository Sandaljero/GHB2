
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// решение вопросов исполнения задач
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.2.7.3") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Файлы. Доступность отдельных команд настраивается при активизации файла в дереве предметов.
	ДоступенЗахватФайлов = ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("1.4.9.1");
	// Мультипредметность.
	ДоступнаМультипредметность = Ложь;
	
	КаталогДляСохраненияДанных = ИнтеграцияС1СДокументооборотВызовСервера.ЛокальныйКаталогФайлов();
	ИмяПользователя = ПараметрыСеанса.ИнтеграцияС1СДокументооборотИмяПользователя;
	
	ТипПроцессаXDTO = "DMBusinessProcessIssuesSolution";
	ОбъектXDTO = ИнтеграцияС1СДокументооборот.ПолучитьОбъектXDTOПроцесса(ТипПроцессаXDTO, Параметры);
	ЗаполнитьФормуИзОбъектаXDTO(ОбъектXDTO);
	
	ИнтеграцияС1СДокументооборотПереопределяемый.ДополнительнаяОбработкаФормыБизнесПроцесса(
		ЭтаФорма,
		Отказ,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтаФорма);
		ТекстПредупреждения = "";
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы,,ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДокументооборотДокумент" И Источник = Элементы.ПредметПредставление Тогда
		Предмет = Параметр.name;
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотБизнесПроцесс" Тогда
		Если Параметр.ID = ID Тогда
			ПеречитатьПроцесс();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбработатьФормуПоНаличиюПредметов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредметРассмотренияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ГлавнаяЗадача) Тогда
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект(ПредметРассмотренияТип, ПредметРассмотренияID, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВажностьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтеграцияС1СДокументооборотКлиент.ВыбратьЗначениеИзВыпадающегоСписка(
		"DMBusinessProcessImportance",
		"Важность",
		ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВажностьАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMBusinessProcessImportance", ДанныеВыбора, Текст, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВажностьОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		ИнтеграцияС1СДокументооборотВызовСервера.ДанныеДляАвтоПодбора(
			"DMBusinessProcessImportance", ДанныеВыбора, Текст, СтандартнаяОбработка);
		Если ДанныеВыбора.Количество() = 1 Тогда
			ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора(
				"Важность", ДанныеВыбора[0].Значение, СтандартнаяОбработка, ЭтаФорма);
			СтандартнаяОбработка = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВажностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтеграцияС1СДокументооборотКлиент.ОбработкаВыбораДанныхДляАвтоПодбора("Важность", ВыбранноеЗначение, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СрокДатаПриИзменении(Элемент)
	
	Если Срок = НачалоДня(Срок) Тогда
		Срок = КонецДня(Срок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйСрокДатаПриИзменении(Элемент)
	
	ПараметрыЗапроса = Новый Структура("Автор, АвторID, АвторТип, СтарыйСрок, НовыйСрок",
		Автор,
		АвторID,
		АвторТип,
		СтарыйСрок,
		НовыйСрок);
	ДлительностьПереноса = ПолучитьПодписьДлительностиПереносаСрока(ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйСрокВремяПриИзменении(Элемент)
	
	ПараметрыЗапроса = Новый Структура("Автор, АвторID, АвторТип, СтарыйСрок, НовыйСрок",
		Автор,
		АвторID,
		АвторТип,
		СтарыйСрок,
		НовыйСрок);
	ДлительностьПереноса = ПолучитьПодписьДлительностиПереносаСрока(ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПредметыИФайлыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДобавитьПредмет(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоПриложений

&НаКлиенте
Процедура ДеревоПриложенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Строка = Элемент.ТекущиеДанные;
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Строка.Ссылка) Тогда // объект ИС
		ПоказатьЗначение(, Строка.Ссылка);
		
	ИначеЕсли Строка.ПредметТип = "DMFile" Тогда // файл
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьФайл(Строка.ПредметID, Строка.Предмет,
			Строка.Расширение, УникальныйИдентификатор);
			
	Иначе // объект ДО
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект(Строка.ПредметТип,Строка.ПредметID, Строка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандДереваПриложений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьБизнесПроцесс(Команда)
	
	ЗаписатьОбъект();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ЗаписатьОбъект() И Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтартоватьИЗакрыть(Команда)
	
	Если ПроверитьЗаполнениеБизнесПроцесса() Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗапуска = ПодготовитьКПередачеИСтартоватьБизнесПроцесс();
	
	Если РезультатЗапуска Тогда
		ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьБизнесПроцесса(ЭтаФорма, Истина);
		Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Бизнес-процесс ""%1"" успешно запущен.'"), Представление));
		Модифицированность = Ложь;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьПроцесс(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ОстановитьПроцесс(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрерватьПроцесс(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ПрерватьПроцесс(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПроцесс(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ПродолжитьПроцесс(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПредмет(Команда)
	
	Если Завершен Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьПредметЗавершение", ЭтаФорма);
	ИнтеграцияС1СДокументооборотКлиент.ДобавитьПредмет(ЭтаФорма, "Вспомогательный", ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПредметЗавершение(ОписаниеПредмета, ПараметрыОбработчика) Экспорт
	
	Если ОписаниеПредмета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьПредметЗавершениеНаСервере(ОписаниеПредмета);
	Модифицированность = Истина;
	ОбработатьФормуПоНаличиюПредметов();
	
	// Развернем дерево и установим фокус на последнюю строку с добавленным предметом.
	ЭлементыДерева = ДеревоПриложений.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		ПоследнийИдентификатор = ЭлементДерева.ПолучитьИдентификатор();
		Элементы.ДеревоПриложений.Развернуть(ПоследнийИдентификатор);
	КонецЦикла;
	Элементы.ДеревоПриложений.ТекущаяСтрока = ПоследнийИдентификатор;
	
	Если ОписаниеПредмета.ПредметТип = "DMFile" Тогда
		ЗаписатьОбъект();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПредметЗавершениеНаСервере(ОписаниеПредмета)
	
	Дерево = РеквизитФормыВЗначение("ДеревоПриложений");
	
	СтрокаПредмета = Дерево.Строки.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаПредмета, ОписаниеПредмета);
	СтрокаПредмета.ДоступноУдаление = Истина;
	Если ЗначениеЗаполнено(СтрокаПредмета.Ссылка) Тогда
		СтрокаПредмета.Представление = Строка(СтрокаПредмета.Ссылка);
	КонецЕсли;
	УстановитьКартинкуПредмета(СтрокаПредмета);
	ДополнитьДеревоПриложенийФайламиПредмета(СтрокаПредмета);
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПриложений");
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПредмет(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ДеревоПриложений.ПолучитьЭлементы().Удалить(ТекущиеДанные);
	КонецЕсли;
	
	ОбработатьФормуПоНаличиюПредметов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточку(Команда)
	
	Строка = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Строка.Ссылка) Тогда // объект ИС
		ПоказатьЗначение(, Строка.Ссылка);
		
	Иначе // объект ДО
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьОбъект(Строка.ПредметТип, Строка.ПредметID, 
			Элементы.ДеревоПриложенийПредставление);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайлДляПросмотра(Команда)
	
	Строка = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если Строка <> Неопределено И Строка.ПредметТип = "DMFile" Тогда
		ИнтеграцияС1СДокументооборотКлиент.ОткрытьФайл(Строка.ПредметID, Строка.Предмет,
			Строка.Расширение, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ПредметТип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	Если ТекущиеДанные.Редактируется И Не ТекущиеДанные.РедактируетсяТекущимПользователем Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Этот файл сейчас редактируется другим пользователем.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Команда", "Редактировать");
	ПараметрыОповещения.Вставить("ИдентификаторСтроки", ТекущиеДанные.ПолучитьИдентификатор());
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КомандыРедактированияЗавершение", ЭтаФорма, ПараметрыОповещения);
	
	ИнтеграцияС1СДокументооборотКлиент.ОткрытьФайл(ТекущиеДанные.ПредметID, ТекущиеДанные.Предмет,
		ТекущиеДанные.Расширение, УникальныйИдентификатор, Ложь, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
			Или ТекущиеДанные.ПредметТип <> "DMFile"
			Или Не ТекущиеДанные.РедактируетсяТекущимПользователем Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Команда", "ЗакончитьРедактирование");
	ПараметрыОповещения.Вставить("ИдентификаторСтроки", ТекущиеДанные.ПолучитьИдентификатор());
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КомандыРедактированияЗавершение", ЭтаФорма, ПараметрыОповещения);
	
	ИнтеграцияС1СДокументооборотКлиент.ЗакончитьРедактированиеФайла(
		ТекущиеДанные.ПредметID,
		ТекущиеДанные.Предмет,
		ТекущиеДанные.Расширение,
		УникальныйИдентификатор,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРедактирование(Команда)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
			Или ТекущиеДанные.ПредметТип <> "DMFile"
			Или Не ТекущиеДанные.РедактируетсяТекущимПользователем Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Команда", "ОтменитьРедактирование");
	ПараметрыОповещения.Вставить("ИдентификаторСтроки", ТекущиеДанные.ПолучитьИдентификатор());
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"КомандыРедактированияЗавершение", ЭтаФорма, ПараметрыОповещения);
	
	ИнтеграцияС1СДокументооборотКлиент.ОтменитьРедактированиеФайла(
		ТекущиеДанные.ПредметID, ОписаниеОповещения);
	
КонецПроцедуры

// Общее завершение команд редактирования.
&НаКлиенте
Процедура КомандыРедактированияЗавершение(Результат, Параметры) Экспорт
	
	ТекущиеДанные = ДеревоПриложений.НайтиПоИдентификатору(Параметры.ИдентификаторСтроки);
	Если Параметры.Команда = "Редактировать" Тогда
		ТекущиеДанные.Редактируется = Истина;
		ТекущиеДанные.РедактируетсяТекущимПользователем = Истина;
	Иначе // "ЗакончитьРедактирование" Или "ОтменитьРедактирование"
		ТекущиеДанные.Редактируется = Ложь;
		ТекущиеДанные.РедактируетсяТекущимПользователем = Ложь;
	КонецЕсли;
	УстановитьДоступностьКомандДереваПриложений();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверитьЗаполнениеБизнесПроцесса()
	
	ЕстьОшибки = Ложь;
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Описание) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			?(ВидВопросаID = "ПереносСрока",
				НСтр("ru = 'Не указано основание для переноса срока.'"),
				НСтр("ru = 'Поле ""Вопрос"" не заполнено'")),,
			"Описание");
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ВидВопросаID = "ПереносСрока" И Не ЗначениеЗаполнено(НовыйСрок) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не введен новый срок выполнения.'"),,
			"НовыйСрок");
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Возврат ЕстьОшибки;
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьКомандДереваПриложений()
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Элементы.ОткрытьФайлДляПросмотра.Доступность = Ложь;
		Элементы.Редактировать.Доступность = Ложь;
		Элементы.ЗакончитьРедактирование.Доступность = Ложь;
		Элементы.ОтменитьРедактирование.Доступность = Ложь;
		Элементы.УдалитьПредмет.Доступность = Ложь;
		Элементы.ФормаУдалитьПредмет.Доступность = Ложь;
	Иначе
		Если ТекущиеДанные.ПредметТип = "DMFile" Тогда
			Элементы.ОткрытьФайлДляПросмотра.Доступность = Истина;
			Элементы.Редактировать.Доступность = ДоступенЗахватФайлов И
				(Не ТекущиеДанные.Редактируется Или ТекущиеДанные.РедактируетсяТекущимПользователем);
			Элементы.ЗакончитьРедактирование.Доступность = ДоступенЗахватФайлов И
				ТекущиеДанные.РедактируетсяТекущимПользователем;
			Элементы.ОтменитьРедактирование.Доступность = ДоступенЗахватФайлов И
				ТекущиеДанные.РедактируетсяТекущимПользователем;
		Иначе
			Элементы.ОткрытьФайлДляПросмотра.Доступность = Ложь;
			Элементы.Редактировать.Доступность = Ложь;
			Элементы.ЗакончитьРедактирование.Доступность = Ложь;
			Элементы.ОтменитьРедактирование.Доступность = Ложь;
		КонецЕсли;
		Элементы.УдалитьПредмет.Доступность = ТекущиеДанные.ДоступноУдаление И Не ТекущиеДанные.Редактируется;
		Элементы.ФормаУдалитьПредмет.Доступность = ТекущиеДанные.ДоступноУдаление И Не ТекущиеДанные.Редактируется;
	КонецЕсли;
	
КонецПроцедуры

// Управляет видимостью элементов формы в зависимости от наличия предметов.
&НаКлиенте
Процедура ОбработатьФормуПоНаличиюПредметов()
	
	ЕстьПредметы = (ДеревоПриложений.ПолучитьЭлементы().Количество() <> 0);
	Если ЕстьПредметы Тогда
		Если Элементы.ГруппаПриложения.ТекущаяСтраница = Элементы.СтраницаНетПредметов Тогда
			Элементы.ГруппаПриложения.ТекущаяСтраница = Элементы.СтраницаЕстьПредметы;
		КонецЕсли;
	Иначе
		Если Элементы.ГруппаПриложения.ТекущаяСтраница = Элементы.СтраницаЕстьПредметы Тогда
			Элементы.ГруппаПриложения.ТекущаяСтраница = Элементы.СтраницаНетПредметов;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФормуИзОбъектаXDTO(ОбъектXDTO, ОбновлятьПредметы = Истина)
	
	Если ОбъектXDTO.Установлено("objectID") Тогда
		ID = ОбъектXDTO.objectId.id;
		Тип = ОбъектXDTO.objectId.type;
	КонецЕсли;
	
	Если Не ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1") Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;
		Элементы.ГруппаПодвал.ТолькоПросмотр = Истина;
		Элементы.ФормаСтартоватьИЗакрыть.Доступность = Ложь;
		Элементы.ФормаЗаписатьИЗакрыть.Доступность = Ложь;
		Элементы.ФормаЗаписать.Доступность = Ложь;
		Элементы.ГруппаУправлениеПроцессом.Доступность = Ложь;
		Элементы.ФормаДобавитьПредмет.Видимость = Ложь;
		Элементы.ФормаУдалитьПредмет.Видимость = Ложь;
	КонецЕсли;
	
	Если ОбъектXDTO.started Тогда
		Элементы.ФормаСтартоватьИЗакрыть.Видимость = Ложь;
		Элементы.ФормаЗаписатьИЗакрыть.Видимость = Истина;
	Иначе
		Элементы.ФормаСтартоватьИЗакрыть.Видимость = Истина;
		Элементы.ФормаЗаписатьИЗакрыть.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ФормаСтартоватьИЗакрыть.КнопкаПоУмолчанию = Элементы.ФормаСтартоватьИЗакрыть.Видимость;
	Элементы.ФормаЗаписатьИЗакрыть.КнопкаПоУмолчанию = Элементы.ФормаЗаписатьИЗакрыть.Видимость;
	
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьНавигационнуюСсылку(ЭтаФорма, ОбъектXDTO);
	Обработки.ИнтеграцияС1СДокументооборот.ЗаполнитьСтандартнуюШапкуБизнесПроцесса(ЭтаФорма, ОбъектXDTO);
	Обработки.ИнтеграцияС1СДокументооборот.УстановитьВидимостьКомандИзмененияСостоянияПроцесса(ЭтаФорма, ОбъектXDTO);
	Обработки.ИнтеграцияС1СДокументооборот.ЗаполнитьОбъектныйРеквизит(ЭтаФорма, ОбъектXDTO.initiator, "ИнициаторПроцесса");
	Обработки.ИнтеграцияС1СДокументооборот.ЗаполнитьОбъектныйРеквизит(ЭтаФорма, ОбъектXDTO.issueTask, "ПредметРассмотрения", Истина);
	
	ЭтаФорма.ТолькоПросмотр = ОбъектXDTO.completed;
	Элементы.НовыйСрокДата.ТолькоПросмотр = ОбъектXDTO.completed;
	Элементы.НовыйСрокВремя.ТолькоПросмотр = ОбъектXDTO.completed;
	
	РезультатВыполнения = ОбъектXDTO.perfomanceHistory;
	
	Длительность = "";
	Если ОбъектXDTO.started Тогда
		Если ОбъектXDTO.completed Тогда
			Длительность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '(выполнялся %1)'"),
				НРег(ИнтеграцияС1СДокументооборотКлиентСервер.РазностьДатВДнях(ДатаЗавершения, ДатаНачала)));
		Иначе
			Длительность = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '(выполняется %1)'"),
				НРег(ИнтеграцияС1СДокументооборотКлиентСервер.РазностьДатВДнях(ТекущаяДатаСеанса(), ДатаНачала)));
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаПереносСрока.Видимость = Ложь;
	Элементы.Описание.ПодсказкаВвода = НСтр("ru = 'Текст вопроса'");
	Элементы.Описание.Подсказка = НСтр("ru = 'Текст вопроса'");
	Элементы.ФормаСтартоватьИЗакрыть.Заголовок = НСтр("ru = 'Задать вопрос и закрыть'");
	
	Если ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "issueType") Тогда
		ВидВопроса = ОбъектXDTO.issueType.name;
		ВидВопросаID = ОбъектXDTO.issueType.objectID.id;
		ВидВопросаТип = ОбъектXDTO.issueType.objectID.type;
		Если ВидВопросаID = "ПереносСрока" Тогда
			Элементы.Описание.ПодсказкаВвода = НСтр("ru = 'Обоснование запроса'");
			Элементы.Описание.Подсказка = НСтр("ru = 'Обоснование запроса'");
			Элементы.ФормаСтартоватьИЗакрыть.Заголовок = НСтр("ru = 'Отправить запрос и закрыть'");
		КонецЕсли;
	ИначеЕсли ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1") Тогда
		ВидВопроса = "Иное";
		ВидВопросаID = "Иное";
		ВидВопросаТип = "DMIssueType";
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "issueTask")
			И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "newDueDate") Тогда
		Элементы.ГруппаПереносСрока.Видимость = Истина;
		СтарыйСрок = ОбъектXDTO.issueTask.dueDate;
		НовыйСрок = ОбъектXDTO.newDueDate;
		
		Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1.CORP") Тогда
			Элементы.ГруппаПереносСрока.ТолькоПросмотр = Ложь;
			Элементы.ДлительностьПереноса.Видимость = Истина;
			ПараметрыЗапроса = Новый Структура("Автор, АвторID, АвторТип, СтарыйСрок, НовыйСрок",
				Автор,
				АвторID,
				АвторТип,
				СтарыйСрок,
				НовыйСрок);
			ДлительностьПереноса = ПолучитьПодписьДлительностиПереносаСрока(ПараметрыЗапроса);
		Иначе
			Элементы.ГруппаПереносСрока.ТолькоПросмотр = Истина;
			Элементы.ДлительностьПереноса.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1") Тогда
		Элементы.ГруппаПриложения.Видимость = Истина;
		Если ОбновлятьПредметы И ИнтеграцияС1СДокументооборот.СвойствоУстановлено(ОбъектXDTO, "files") Тогда
			ЗаполнитьДеревоПриложенийПоФайлам(ОбъектXDTO.files);
		КонецЕсли;
	Иначе
		Элементы.ГруппаПриложения.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПриложенийПоФайлам(ФайлыXDTO)
	
	Дерево = РеквизитФормыВЗначение("ДеревоПриложений");
	
	ДополнитьДеревоПриложенийФайлами(Дерево.Строки, ФайлыXDTO, Истина);
	
	ЗначениеВРеквизитФормы(Дерево, "ДеревоПриложений");
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьДеревоПриложенийФайлами(СтрокиДерева, ФайлыXDTO, ДоступноУдаление = Ложь)
	
	СтрокиДерева.Очистить();
	
	Для Каждого ФайлXDTO Из ФайлыXDTO Цикл
		СтрокаФайла = СтрокиДерева.Добавить();
		СтрокаФайла.Предмет = ФайлXDTO.name;
		СтрокаФайла.ПредметТип = ФайлXDTO.objectId.type;
		СтрокаФайла.ПредметID = ФайлXDTO.objectId.id;
		СтрокаФайла.Картинка = РаботаСФайламиСлужебныйКлиентСервер.
			ПолучитьИндексПиктограммыФайла(ФайлXDTO.extension);
		СтрокаФайла.Расширение = ФайлXDTO.extension;
		Если ДоступенЗахватФайлов Тогда
			СтрокаФайла.Редактируется = ФайлXDTO.editing;
			Если ФайлXDTO.Установлено("editingUser") Тогда
				СтрокаФайла.РедактируетсяТекущимПользователем =
					(ВРег(ФайлXDTO.editingUser.name) = ВРег(ИмяПользователя));
			КонецЕсли;
		КонецЕсли;
		СтрокаФайла.Представление = СтрокаФайла.Предмет;
		СтрокаФайла.ДоступноУдаление = ДоступноУдаление;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьКПередачеИЗаписатьБизнесПроцесс()
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ОбъектXDTO = ПодготовитьБизнесПроцесс(Прокси);
	
	Если ЗначениеЗаполнено(ID) Тогда
		РезультатЗаписи = ИнтеграцияС1СДокументооборот.ЗаписатьОбъект(Прокси, ОбъектXDTO);
	Иначе
		РезультатСоздания = ИнтеграцияС1СДокументооборот.СоздатьНовыйОбъект(Прокси, ОбъектXDTO);
	КонецЕсли;
	
	Результат = ?(РезультатСоздания = Неопределено, РезультатЗаписи, РезультатСоздания);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Результат);
	
	Если РезультатЗаписи <> Неопределено Тогда
		УстановитьСсылкуБизнесПроцесса(Результат.objects[0]);
	Иначе
		УстановитьСсылкуБизнесПроцесса(Результат.object);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ПодготовитьБизнесПроцесс(Прокси)
	
	ОбъектXDTO = Обработки.ИнтеграцияС1СДокументооборот.ПодготовитьШапкуБизнесПроцесса(
		Прокси,
		"DMBusinessProcessIssuesSolution",
		ЭтаФорма,
		"Шаблон");
	
	//специфика Поручения
	
	Обработки.ИнтеграцияС1СДокументооборот.ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(Прокси,
		ЭтаФорма,
		"ИнициаторПроцесса",
		ОбъектXDTO.initiator,
		"DMUser");
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	Задача = ИнтеграцияС1СДокументооборот.ПолучитьОбъект(Прокси, ПредметРассмотренияТип, ПредметРассмотренияID);
	ЗадачаПриемник = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси,"DMBusinessProcessTask");
	ИнтеграцияС1СДокументооборот.ЗаполнитьЗначенияСвойствXDTO(Прокси,ЗадачаПриемник,Задача.Objects[0]);
	ОбъектXDTO.issueTask = ЗадачаПриемник;
	Если ИнтеграцияС1СДокументооборот.СвойствоСуществует(ОбъектXDTO, "issueType") Тогда
		Обработки.ИнтеграцияС1СДокументооборот.ЗаполнитьОбъектXDTOИзОбъектногоРеквизита(Прокси,
			ЭтаФорма,
			"ВидВопроса",
			ОбъектXDTO.issueType,
			ВидВопросаТип);
	КонецЕсли;
	Если ИнтеграцияС1СДокументооборот.СвойствоСуществует(ОбъектXDTO, "newDueDate") Тогда
		ОбъектXDTO.newDueDate = НовыйСрок;
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотПовтИсп.ДоступенФункционалВерсииСервиса("2.1.18.1") Тогда
		ЗаполнитьПредметамиИзДереваПриложений(Прокси, ДеревоПриложений.ПолучитьЭлементы(), ОбъектXDTO);
	КонецЕсли;
	
	Возврат ОбъектXDTO;
	
КонецФункции

&НаКлиенте
Функция ЗаписатьОбъект() Экспорт
	
	Если ПроверитьЗаполнениеБизнесПроцесса() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПодготовитьКПередачеИЗаписатьБизнесПроцесс();
	ИнтеграцияС1СДокументооборотКлиент.Оповестить_ЗаписьБизнесПроцесса(ЭтаФорма, Ложь);
	Заголовок = Представление;
	Состояние(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Бизнес-процесс ""%1"" сохранен.'"), Представление));
	Модифицированность = Ложь;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура УстановитьСсылкуБизнесПроцесса(ОбъектXDTO)
	
	ID = ОбъектXDTO.objectId.id;
	Если ОбъектXDTO.objectId.Свойства().Получить("presentation") <> Неопределено Тогда
		Представление = ОбъектXDTO.objectId.presentation;
	Иначе
		Представление = ОбъектXDTO.name;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
	ЗаписатьОбъект();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПрограммноДобавленнуюКоманду(Команда)
	
	// Вызовем обработчик команды, которая добавлена программно при создании формы на сервере.
	ИнтеграцияС1СДокументооборотКлиентПереопределяемый.ВыполнитьПрограммноДобавленнуюКоманду(Команда, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьПроцесс()
	
	ПараметрыПолучения = Новый Структура("id, type", ID, Тип);
	ОбъектXDTO = ИнтеграцияС1СДокументооборот.ПолучитьОбъектXDTOПроцесса(Тип, ПараметрыПолучения);
	ЗаполнитьФормуИзОбъектаXDTO(ОбъектXDTO, Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПодписьДлительностиПереносаСрока(ПараметрыЗапроса)
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	
	Запрос = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMPostponementDurationRequest");
	Запрос.user = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMUser");
	Запрос.user.name = ПараметрыЗапроса.Автор;
	Запрос.user.objectId = ИнтеграцияС1СДокументооборот.СоздатьObjectID(Прокси,
		ПараметрыЗапроса.АвторID, ПараметрыЗапроса.АвторТип);
	Запрос.oldDueDate = ПараметрыЗапроса.СтарыйСрок;
	Запрос.newDueDate = ПараметрыЗапроса.НовыйСрок;
	
	Ответ = Прокси.execute(Запрос);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Возврат Ответ.postponementDuration;
	
КонецФункции

// Устанавливает картинку предмета по его роли или по расширению файла.
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКартинкуПредмета(СтрокаПредмета)
	
	Если СтрокаПредмета.ПредметТип = "DMFile" Тогда // для файла – по расширению
		СтрокаПредмета.Картинка = РаботаСФайламиСлужебныйКлиентСервер.
			ПолучитьИндексПиктограммыФайла(СтрокаПредмета.Расширение);
	Иначе // для прочих типов – по роли
		СтрокаПредмета.Картинка = ИнтеграцияС1СДокументооборотКлиентСервер.КартинкаПоРолиПредмета(
			СтрокаПредмета.РольПредмета);
	КонецЕсли;
	
КонецПроцедуры

// Получает вызовом сервиса файлы предмета и дополняет ими дерево приложений.
//
&НаСервере
Процедура ДополнитьДеревоПриложенийФайламиПредмета(СтрокаПредмета)
	
	Если СтрокаПредмета.ПредметТип = "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	СписокФайлов = ИнтеграцияС1СДокументооборотВызовСервера.ФайлыПоВладельцу(
		СтрокаПредмета.ПредметID, СтрокаПредмета.Предмет, СтрокаПредмета.ПредметТип);
	ДополнитьДеревоПриложенийФайлами(СтрокаПредмета.Строки, СписокФайлов.files);
	
КонецПроцедуры

// Заполняет объект XDTO задачи бизнес-процесса предметами из дерева приложений формы.
//
&НаСервере
Процедура ЗаполнитьПредметамиИзДереваПриложений(Прокси, СтрокиДерева, ОбъектXDTO)
	
	ОбъектXDTO.files.Очистить();
	
	Для Каждого СтрокаПредмета Из СтрокиДерева Цикл
		
		file = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, СтрокаПредмета.ПредметТип);
		file.name = СтрокаПредмета.Предмет;
		file.objectId = ИнтеграцияС1СДокументооборот.СоздатьОбъект(Прокси, "DMObjectID");
		file.objectId.id = СтрокаПредмета.ПредметID;
		file.objectId.type = СтрокаПредмета.ПредметТип;
		
		ОбъектXDTO.files.Добавить(file);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьКПередачеИСтартоватьБизнесПроцесс()
	
	Прокси = ИнтеграцияС1СДокументооборотПовтИсп.ПолучитьПрокси();
	ОбъектXDTO = ПодготовитьБизнесПроцесс(Прокси);
	
	РезультатЗапуска = ИнтеграцияС1СДокументооборот.ЗапуститьБизнесПроцесс(Прокси, ОбъектXDTO);
	ИнтеграцияС1СДокументооборот.ПроверитьВозвратВебСервиса(Прокси, РезультатЗапуска);
	
	УстановитьСсылкуБизнесПроцесса(РезультатЗапуска.businessProcess);
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти