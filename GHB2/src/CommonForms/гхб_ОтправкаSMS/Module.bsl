
#Область ДействияКоманд

&НаКлиенте
Процедура УстановитьФлаги(Команда)
	
	Для каждого СтрокаПолучатели Из тзПолучатели Цикл
		
		Если ЗначениеЗаполнено(СтрокаПолучатели.ФизЛицо) Тогда
			СтрокаПолучатели.ДН = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлаги(Команда)
	
	Для каждого СтрокаПолучатели Из тзПолучатели Цикл
		СтрокаПолучатели.ДН = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	Если Не ПроверитьНаРабочуюБазуСервер() Тогда
	
		ОбщегоНазначенияКлиент.СообщитьПользователю("Рассылка возможна только из рабочей базы!");
		Возврат;
	
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(ТекстСообщения) Тогда
	
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнен текст сообщения!");
		Возврат;
	
	КонецЕсли;
	
	ОтправитьНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаРабочуюБазуСервер()

	Возврат гхб_ОбщегоНазначенияСервер.ПроверитьНаРабочуюБазу();

КонецФункции // ПроверитьНаРабочуюБазу()

&НаСервере
Процедура ОтправитьНаСервере()
	
	мНайденное = тзПолучатели.НайтиСтроки(Новый Структура("ДН", Истина));
	
	Для каждого СтрокаНайденное Из мНайденное Цикл
	
		Если СтрокаНайденное.Отправлено Тогда
			Продолжить;
		КонецЕсли;
		
		стрОтвет = ОтправитьСМС(СтрокаНайденное.Телефон);
		
		Если СтрЧислоВхождений(стрОтвет, "ACCEPT") > 0 Тогда
			СтрокаНайденное.Отправлено = Истина;
		Иначе
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОтправитьСМС(Телефон) Экспорт
	
	Попытка
		Возврат гхб_РассылкаУведомленийСервер.ОтправитьСМСНаОдинНомер(Телефон, "", ТекстСообщения, ОтправлятьВТранслите);
	Исключение
		
		ЗаписьЖурналаРегистрации("Оправка СМС", УровеньЖурналаРегистрации.Ошибка,,,Телефон + ": " + ОписаниеОшибки());
		Возврат "error";
		
	КонецПопытки;
	
КонецФункции // РазослатьПисьмаИзОчереди(

&НаКлиенте
Процедура Подбор(Команда)
	
	ОткрытьФорму("Справочник.гхб_ФизическиеЛица.Форма.ФормаВыбора", Новый Структура("НеЗакрыватьПриВыборе", Истина), ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#Область СобытияФормыЭлементовФормы

&НаКлиенте
Процедура тзПолучателиФизЛицоПриИзменении(Элемент)
	
	текДанные = Элементы.тзПолучатели.ТекущиеДанные;
	
	стСтрока = ПолучитьДанныеДляСтрокаОтправкиСМС(текДанные.ФизЛицо);
	
	Если стСтрока.ЕстьДанные Тогда
		ЗаполнитьЗначенияСвойств(текДанные, стСтрока);
	Иначе
		
		текДанные.Телефон = Неопределено;
		текДанные.ВидТелефона = Неопределено;
		ОбщегоНазначенияКлиент.СообщитьПользователю("Нет данных по телефонам физ лица " + Строка(текДанные.ФизЛицо));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДляСтрокаОтправкиСМС(ФизЛицо)
	Возврат гхб_ОбщегоНазначенияСервер.ПолучитьДанныеДляСтрокаОтправкиСМС(ФизЛицо);
КонецФункции // ПолучитьДанныеДляСтрокаОтправкиСМС()

&НаКлиенте
Процедура тзПолучателиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Запрещено копирование строк!",,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстСообщенияПриИзменении(Элемент)
	ДлинаСообщенияСимволов = СтрДлина(ТекстСообщения);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура тзПолучателиТелефонНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	текДанные = Элементы.тзПолучатели.ТекущиеДанные;
	ДанныеВыбора = ПолучитьСписокВыборкаТелефонов(текДанные.ФизЛицо);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокВыборкаТелефонов(ФизЛицо)
	Возврат гхб_ОбщегоНазначенияСервер.ПолучитьСписокТелефоновПользователя(ФизЛицо);
КонецФункции // ПолучитьСписокВыборкаТелефонов()

&НаКлиенте
Процедура тзПолучателиТелефонПриИзменении(Элемент)
	
	текДанные = Элементы.тзПолучатели.ТекущиеДанные;
	текДанные.ВидТелефона = ПолучитьВидТелефона(текДанные.ФизЛицо, текДанные.Телефон);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВидТелефона(ФизЛицо, Телефон)
	Возврат гхб_ОбщегоНазначенияСервер.ПолучитьВидТелефона(ФизЛицо, Телефон);
КонецФункции // тзПолучателиТелефонПриИзменении()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//Пока залочил, отправку русского текста, не уходят длинные сообщения.
	ОтправлятьВТранслите = Истина;
КонецПроцедуры

#КонецОбласти

#Область Прочее

#КонецОбласти


