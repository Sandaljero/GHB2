
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура УдалитьЗаписиПоДокументуИПересчитатьДанные(_Документ) Экспорт
	
	ЗапросПересчет = Новый Запрос();
	ЗапросПересчет.Текст = "ВЫБРАТЬ
	                       |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение КАК Помещение,
	                       |	МИНИМУМ(гхб_АктуальныеДокументыКонсервацииПомещений.Дата) КАК Дата1,
	                       |	МАКСИМУМ(гхб_АктуальныеДокументыКонсервацииПомещений.Дата) КАК Дата2
	                       |ПОМЕСТИТЬ _Данные
	                       |ИЗ
	                       |	РегистрСведений.гхб_АктуальныеДокументыКонсервацииПомещений КАК гхб_АктуальныеДокументыКонсервацииПомещений
	                       |ГДЕ
	                       |	гхб_АктуальныеДокументыКонсервацииПомещений.ДокументКонсервации = &ДокументКонсервации
	                       |
	                       |СГРУППИРОВАТЬ ПО
	                       |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение
	                       |
	                       |ИНДЕКСИРОВАТЬ ПО
	                       |	Помещение
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.Ссылка КАК Ссылка,
	                       |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.Помещение КАК Помещение,
	                       |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаНачала КАК ДатаНачала,
	                       |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаОкончания КАК ДатаОкончания,
	                       |	_Данные.Дата1 КАК Дата1,
	                       |	_Данные.Дата2 КАК Дата2
	                       |ИЗ
	                       |	Документ.гхб_ЗакреплениеСтатусаКонсервацииПомещения КАК гхб_ЗакреплениеСтатусаКонсервацииПомещения
	                       |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ _Данные КАК _Данные
	                       |		ПО (гхб_ЗакреплениеСтатусаКонсервацииПомещения.Помещение = _Данные.Помещение
	                       |				И (гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаНачала МЕЖДУ _Данные.Дата1 И _Данные.Дата2
	                       |					ИЛИ гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаОкончания МЕЖДУ _Данные.Дата1 И _Данные.Дата2
	                       |					ИЛИ _Данные.Дата1 МЕЖДУ гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаНачала И гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаОкончания
	                       |					ИЛИ _Данные.Дата2 МЕЖДУ гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаНачала И гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаОкончания))
	                       |ГДЕ
	                       |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.Проведен
	                       |	И гхб_ЗакреплениеСтатусаКонсервацииПомещения.Ссылка <> &ДокументКонсервации
	                       |
	                       |УПОРЯДОЧИТЬ ПО
	                       |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.Дата
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |УНИЧТОЖИТЬ _Данные";
	
	ЗапросПересчет.УстановитьПараметр("ДокументКонсервации", _Документ);
	
	ВыборкаПересчет = ЗапросПересчет.Выполнить().Выбрать();
	
	_ПараметрыУдаления = Новый Структура();
	_ПараметрыУдаления.Вставить("ДокументКонсервации", _Документ);
	
	УдалитьЗаписи(_ПараметрыУдаления);
	
	Пока ВыборкаПересчет.Следующий() Цикл
		
		_Тмп1 = ВыборкаПересчет.ДатаНачала;
		_Тмп2 = ВыборкаПересчет.ДатаОкончания;
		
		ЗаписатьДанныеПоДокументу(ВыборкаПересчет.Ссылка, ВыборкаПересчет.Помещение, _Тмп1, _Тмп2);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьЗаписи(_Параметры) 
	
	ЗапросУдаление = Новый Запрос();
	ЗапросУдаление.Текст = "ВЫБРАТЬ
	                       |	гхб_АктуальныеДокументыКонсервацииПомещений.Дата КАК Дата,
	                       |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение КАК Помещение
	                       |ИЗ
	                       |	РегистрСведений.гхб_АктуальныеДокументыКонсервацииПомещений КАК гхб_АктуальныеДокументыКонсервацииПомещений
	                       |ГДЕ";
	
	Если _Параметры.Свойство("ДокументКонсервации") Тогда
		ЗапросУдаление.Текст = ЗапросУдаление.Текст + "
	                       |	гхб_АктуальныеДокументыКонсервацииПомещений.ДокументКонсервации = &ДокументКонсервации";
		
		ЗапросУдаление.УстановитьПараметр("ДокументКонсервации", _Параметры.ДокументКонсервации);
	Иначе
		ЗапросУдаление.Текст = ЗапросУдаление.Текст + "
	                       |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение = &Помещение
						   |	И гхб_АктуальныеДокументыКонсервацииПомещений.Дата > &Дата";
		
		ЗапросУдаление.УстановитьПараметр("Помещение", _Параметры.Помещение);
		ЗапросУдаление.УстановитьПараметр("Дата", _Параметры.Дата);
	КонецЕсли;
	
	ВыборкаУдаление = ЗапросУдаление.Выполнить().Выбрать();
	
	Пока ВыборкаУдаление.Следующий() Цикл
		
		МЗ = РегистрыСведений.гхб_АктуальныеДокументыКонсервацииПомещений.СоздатьМенеджерЗаписи();
		МЗ.Дата = ВыборкаУдаление.Дата;
		МЗ.Помещение = ВыборкаУдаление.Помещение;
		МЗ.Удалить();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьДанныеПоДокументу(_Документ, _Помещение, Знач _Дата1, Знач _Дата2) Экспорт 
	
	_ПараметрыУдаления = Новый Структура();
	_ПараметрыУдаления.Вставить("Помещение", _Помещение);
	_ПараметрыУдаления.Вставить("Дата", _Дата2);
	
	УдалитьЗаписи(_ПараметрыУдаления);
	
	_Дата1 = НачалоНедели(_Дата1);
	
	Пока _Дата1 <= _Дата2 Цикл
			
		МЗ = РегистрыСведений.гхб_АктуальныеДокументыКонсервацииПомещений.СоздатьМенеджерЗаписи();
		МЗ.Дата = _Дата1;
		МЗ.Помещение = _Помещение;
		МЗ.ДокументКонсервации = _Документ;
		МЗ.Записать();
		
		_Дата1 = гхб_ОбщегоНазначенияКлиентСервер.ДобавитьДеньКДате(_Дата1, 7);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПерезаписатьСтатусыПоВсемДокументам() Экспорт 
	
	НЗ = РегистрыСведений.гхб_АктуальныеДокументыКонсервацииПомещений.СоздатьНаборЗаписей();
	НЗ.Записать();
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.Ссылка КАК Ссылка,
	               |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.Помещение КАК Помещение,
	               |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаНачала КАК ДатаНачала,
	               |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.ДатаОкончания КАК ДатаОкончания
	               |ИЗ
	               |	Документ.гхб_ЗакреплениеСтатусаКонсервацииПомещения КАК гхб_ЗакреплениеСтатусаКонсервацииПомещения
	               |ГДЕ
	               |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.Проведен
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	гхб_ЗакреплениеСтатусаКонсервацииПомещения.Дата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаписатьДанныеПоДокументу(Выборка.Ссылка, Выборка.Помещение, Выборка.ДатаНачала, ?(ЗначениеЗаполнено(Выборка.ДатаОкончания), Выборка.ДатаОкончания, гхб_ОбщегоНазначенияКлиентСервер.ДобавитьДеньКДате(Выборка.ДатаНачала, 6)));
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВернутьСтатусыКонсервацииВПериоде(_Дата1, _Дата2, _Помещения) Экспорт 
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	_Даты.Дата КАК Дата,
	               |	_Даты.ДатаСтрокаЗаголовка КАК ДатаСтрокаЗаголовка,
	               |	_Даты.СтатусКонсервации КАК СтатусКонсервации,
	               |	_Даты.Помещение КАК Помещение
	               |ПОМЕСТИТЬ _Помещения
	               |ИЗ
	               |	&ТЗПериоды КАК _Даты
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Дата
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(гхб_АктуальныеДокументыКонсервацииПомещений.Дата, НЕДЕЛЯ) КАК Дата,
	               |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение КАК Помещение,
	               |	МАКСИМУМ(гхб_АктуальныеДокументыКонсервацииПомещений.ДокументКонсервации.СтатусКонсервации) КАК ДокументКонсервацииСтатусКонсервации
	               |ПОМЕСТИТЬ _Периоды
	               |ИЗ
	               |	РегистрСведений.гхб_АктуальныеДокументыКонсервацииПомещений КАК гхб_АктуальныеДокументыКонсервацииПомещений
	               |ГДЕ
	               |	гхб_АктуальныеДокументыКонсервацииПомещений.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	НАЧАЛОПЕРИОДА(гхб_АктуальныеДокументыКонсервацииПомещений.Дата, НЕДЕЛЯ),
	               |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Помещение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(_Периоды.Дата, _Помещения.Дата) КАК Дата,
				   |	_Помещения.ДатаСтрокаЗаголовка КАК ДатаСтрокаЗаголовка,
	               |	_Помещения.Помещение КАК Помещение,
	               |	ЕСТЬNULL(_Периоды.ДокументКонсервацииСтатусКонсервации, _Помещения.СтатусКонсервации) КАК ДокументКонсервацииСтатусКонсервации
	               |ИЗ
	               |	_Помещения КАК _Помещения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ _Периоды КАК _Периоды
	               |		ПО _Помещения.Помещение = _Периоды.Помещение И _Помещения.Дата = _Периоды.Дата
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ _Периоды
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ _Помещения";
	
	_ТЗПериоды = ВернутьПериодыДляОтчета(_Дата1, _Дата2, _Помещения);
	
	Запрос.УстановитьПараметр("ДатаНачала", _Дата1);
	Запрос.УстановитьПараметр("ДатаОкончания", _Дата2);
	Запрос.УстановитьПараметр("ТЗПериоды", _ТЗПериоды);
	
	_ТЗ = Запрос.Выполнить().Выгрузить();
	_ТЗ.Индексы.Добавить("Помещение");
	
	ЗапросМаксимальныеСроки = Новый Запрос();
	ЗапросМаксимальныеСроки.Текст = "ВЫБРАТЬ
	                                |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение КАК Помещение,
	                                |	МАКСИМУМ(гхб_АктуальныеДокументыКонсервацииПомещений.Дата) КАК Дата
	                                |ПОМЕСТИТЬ _Помещения
	                                |ИЗ
	                                |	РегистрСведений.гхб_АктуальныеДокументыКонсервацииПомещений КАК гхб_АктуальныеДокументыКонсервацииПомещений
	                                |ГДЕ
	                                |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение В(&Помещение)
	                                |
	                                |СГРУППИРОВАТЬ ПО
	                                |	гхб_АктуальныеДокументыКонсервацииПомещений.Помещение
	                                |
	                                |ИНДЕКСИРОВАТЬ ПО
	                                |	Помещение,
	                                |	Дата
	                                |;
	                                |
	                                |////////////////////////////////////////////////////////////////////////////////
	                                |ВЫБРАТЬ
	                                |	_Помещения.Помещение КАК Помещение,
	                                |	гхб_АктуальныеДокументыКонсервацииПомещений.Дата КАК Дата,
	                                |	гхб_АктуальныеДокументыКонсервацииПомещений.ДокументКонсервации.СтатусКонсервации КАК СтатусКонсервации
	                                |ИЗ
	                                |	РегистрСведений.гхб_АктуальныеДокументыКонсервацииПомещений КАК гхб_АктуальныеДокументыКонсервацииПомещений
	                                |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ _Помещения КАК _Помещения
	                                |		ПО гхб_АктуальныеДокументыКонсервацииПомещений.Помещение = _Помещения.Помещение
	                                |			И гхб_АктуальныеДокументыКонсервацииПомещений.Дата = _Помещения.Дата
	                                |ГДЕ
	                                |	гхб_АктуальныеДокументыКонсервацииПомещений.ДокументКонсервации.СтатусКонсервации <> ЗНАЧЕНИЕ(Перечисление.гхб_СтатусыКонсервацииПомещений.GREEN)
	                                |	И гхб_АктуальныеДокументыКонсервацииПомещений.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	                                |
	                                |СГРУППИРОВАТЬ ПО
	                                |	_Помещения.Помещение,
	                                |	гхб_АктуальныеДокументыКонсервацииПомещений.Дата,
	                                |	гхб_АктуальныеДокументыКонсервацииПомещений.ДокументКонсервации.СтатусКонсервации
	                                |;
	                                |
	                                |////////////////////////////////////////////////////////////////////////////////
	                                |УНИЧТОЖИТЬ _Помещения";
	
	ЗапросМаксимальныеСроки.УстановитьПараметр("ДатаНачала", _Дата1);
	ЗапросМаксимальныеСроки.УстановитьПараметр("ДатаОкончания", _Дата2);
	ЗапросМаксимальныеСроки.УстановитьПараметр("Помещение", _Помещения);
	
	_ПомещенияМаксимальныеСроки = ЗапросМаксимальныеСроки.Выполнить().Выгрузить();
	
	Для Каждого Стр Из _ПомещенияМаксимальныеСроки Цикл
		Если ЗначениеЗаполнено(Стр.СтатусКонсервации) И НЕ (Стр.СтатусКонсервации = Перечисления.гхб_СтатусыКонсервацииПомещений.Green) Тогда
			_НайденныеСтроки = _ТЗ.НайтиСтроки(Новый Структура("Помещение", Стр.Помещение));
			
			Для Каждого _НС Из _НайденныеСтроки Цикл
				Если (_НС.Дата > Стр.Дата) Тогда 
					_НС.ДокументКонсервацииСтатусКонсервации = Перечисления.гхб_СтатусыКонсервацииПомещений.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат _ТЗ;
	
КонецФункции

Функция ВернутьПериодыДляОтчета(Знач Дата1, Знач Дата2, _Помещения)
	
	Дата1 = НачалоНедели(Дата1);
	Дата2 = НачалоДня(КонецНедели(Дата2));
	
	_ТЗ = Новый ТаблицаЗначений();
	_ТЗ.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата", , , , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	_ТЗ.Колонки.Добавить("ДатаСтрокаЗаголовка", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)));
	_ТЗ.Колонки.Добавить("СтатусКонсервации", Новый ОписаниеТипов("ПеречислениеСсылка.гхб_СтатусыКонсервацииПомещений"));
	_ТЗ.Колонки.Добавить("Помещение", Новый ОписаниеТипов("СправочникСсылка.гхб_Помещения"));
	
	Для Каждого _Помещение Из _Помещения Цикл
		
		_ТмпДата1 = Дата1;
		
		Пока _ТмпДата1 <= Дата2 Цикл
			
			НС = _ТЗ.Добавить();
			НС.Дата = _ТмпДата1;
			НС.ДатаСтрокаЗаголовка = Формат(_ТмпДата1, "ДФ=dd.MM") + "-" + Символы.ПС + Формат(КонецНедели(_ТмпДата1), "ДФ=dd.MM");
			НС.СтатусКонсервации = Перечисления.гхб_СтатусыКонсервацииПомещений.Green;
			НС.Помещение = _Помещение;
			
			_ТмпДата1 = гхб_ОбщегоНазначенияКлиентСервер.ДобавитьДеньКДате(_ТмпДата1, 7);
			
		КонецЦикла;
	
	КонецЦикла;
	
	Возврат _ТЗ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли