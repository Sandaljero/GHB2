
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВернутьАктуальныеПомещенияДляОтветственного(_ОтветственныйЗаПомещения, _СписокЛокаций = Неопределено) Экспорт 
	
	_Рез = Новый Массив();
	
	Если ЗначениеЗаполнено(_ОтветственныйЗаПомещения) Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	гхб_ОтветственныеЗаПомещенияСрезПоследних.Помещение КАК Помещение
		               |ИЗ
		               |	РегистрСведений.гхб_ОтветственныеЗаПомещения.СрезПоследних КАК гхб_ОтветственныеЗаПомещенияСрезПоследних
		               |ГДЕ
		               |	гхб_ОтветственныеЗаПомещенияСрезПоследних.ОтветственныйЗаПомещение = &ОтветственныйЗаПомещение
		               |	И НЕ гхб_ОтветственныеЗаПомещенияСрезПоследних.Закрыть";
		
		Если НЕ (_СписокЛокаций = Неопределено) Тогда
			Запрос.Текст = Запрос.Текст + "
				|	И гхб_ОтветственныеЗаПомещенияСрезПоследних.Помещение В ИЕРАРХИИ (&СписокЛокаций)";
			
			Запрос.УстановитьПараметр("СписокЛокаций", _СписокЛокаций);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ОтветственныйЗаПомещение", _ОтветственныйЗаПомещения);
		
		_Рез = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Помещение");
		
	КонецЕсли;
	
	Возврат _Рез;
	
КонецФункции

Функция ВернутьОтветственногоДляПомещения(_Помещение) Экспорт 
	
	_Рез = Справочники.гхб_ФизическиеЛица.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(_Помещение) Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	гхб_ОтветственныеЗаПомещенияСрезПоследних.ОтветственныйЗаПомещение КАК ОтветственныйЗаПомещение
		               |ИЗ
		               |	РегистрСведений.гхб_ОтветственныеЗаПомещения.СрезПоследних(, Помещение = &Помещение) КАК гхб_ОтветственныеЗаПомещенияСрезПоследних
		               |ГДЕ
		               |	НЕ гхб_ОтветственныеЗаПомещенияСрезПоследних.Закрыть";
		
		Запрос.УстановитьПараметр("Помещение", _Помещение);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			_Рез = Выборка.ОтветственныйЗаПомещение;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат _Рез;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли