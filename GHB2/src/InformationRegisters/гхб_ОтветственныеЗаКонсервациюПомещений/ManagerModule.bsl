
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВернутьАктуальныеПомещенияДляОтветственногоЗаКонсервацию(_ОтветственныйЗаКонсервациюПомещения, _СписокЛокаций = Неопределено) Экспорт 
	
	_Рез = Новый Массив();
	
	Если ЗначениеЗаполнено(_ОтветственныйЗаКонсервациюПомещения) Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.Помещение КАК Помещение
		               |ИЗ
		               |	РегистрСведений.гхб_ОтветственныеЗаКонсервациюПомещений.СрезПоследних(, ОтветственныйЗаКонсервациюПомещения = &ОтветственныйЗаКонсервациюПомещения) КАК гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних
		               |ГДЕ
		               |	НЕ гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.Закрыть";
		
		Если НЕ (_СписокЛокаций = Неопределено) Тогда
			Запрос.Текст = Запрос.Текст + "
				|	И гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.Помещение В ИЕРАРХИИ (&СписокЛокаций)";
			
			Запрос.УстановитьПараметр("СписокЛокаций", _СписокЛокаций);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ОтветственныйЗаКонсервациюПомещения", _ОтветственныйЗаКонсервациюПомещения);
		
		_Рез = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Помещение");
		
	КонецЕсли;
	
	Возврат _Рез;
	
КонецФункции

Функция ВернутьОтветственныхЗаКонсервациюПомещения(_Помещение) Экспорт 
	
	_Рез = Новый Массив();
	
	Если ЗначениеЗаполнено(_Помещение) Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.ОтветственныйЗаКонсервациюПомещения КАК ОтветственныйЗаКонсервациюПомещения
		               |ИЗ
		               |	РегистрСведений.гхб_ОтветственныеЗаКонсервациюПомещений.СрезПоследних(, Помещение = &Помещение) КАК гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних
		               |ГДЕ
		               |	НЕ гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.Закрыть";
		
		Запрос.УстановитьПараметр("Помещение", _Помещение);
		
		_Рез = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОтветственныйЗаКонсервациюПомещения");
		
	КонецЕсли;
	
	Возврат _Рез;
	
КонецФункции

Функция ПроверитьЗакреплениеОтветственногоЗаПомещением(_Помещение, _ОтветственныйЗаКонсервациюПомещения) Экспорт 
	
	_Результат = Ложь;
	
	Если ЗначениеЗаполнено(_Помещение) И ЗначениеЗаполнено(_ОтветственныйЗаКонсервациюПомещения) Тогда 
	
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.Закрыть КАК Закрыть
		               |ИЗ
		               |	РегистрСведений.гхб_ОтветственныеЗаКонсервациюПомещений.СрезПоследних(
		               |			,
		               |			ОтветственныйЗаКонсервациюПомещения = &ОтветственныйЗаКонсервациюПомещения
		               |				И Помещение = &Помещение) КАК гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних
		               |ГДЕ
		               |	НЕ гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.Закрыть";
		
		Запрос.УстановитьПараметр("ОтветственныйЗаКонсервациюПомещения", _ОтветственныйЗаКонсервациюПомещения);
		Запрос.УстановитьПараметр("Помещение", _Помещение);
		
		_Результат = НЕ Запрос.Выполнить().Пустой();
	
	КонецЕсли;
	
	Возврат _Результат;
	
КонецФункции

Функция ВернутьПомещенияДляОтбораПоКонсервацииТекущийПользователь() Экспорт 
	
	Если РольДоступна(Метаданные.Роли.ПолныеПрава) Тогда
		_Массив = Справочники.гхб_Помещения.ВернутьАктивныеПомещенияПоТипу(Справочники.гхб_ТипыПомещений.Локация);
	Иначе
		_ФЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСеанса.ТекущийПользователь, "ФизическоеЛицо");
		
		_Массив = РегистрыСведений.гхб_РуководителиЛокаций.ВернутьЛокацииПоРуководителю(_ФЛ);
		
		_МассивЛокацийОтветственные = РегистрыСведений.гхб_ОтветственныеЗаЛокации.ВернутьЛокацииПоОтветственному(_ФЛ);
		
		Для Каждого _Локация Из _МассивЛокацийОтветственные Цикл
			Если _Массив.Найти(_Локация) = Неопределено Тогда
				_Массив.Добавить(_Локация);
			КонецЕсли;
		КонецЦикла;
		
		_МассивДополнительный = РегистрыСведений.гхб_ДополнительныеОтветственныеЗаЛокации.ВернутьЛокацииПоДополнительномуОтветственному(_ФЛ);
		
		Для Каждого _Локация Из _МассивДополнительный Цикл
			Если (_Массив.Найти(_Локация) = Неопределено) Тогда
				_Массив.Добавить(_Локация);
			КонецЕсли;
		КонецЦикла;
		
		_МассивПомещения = РегистрыСведений.гхб_ОтветственныеЗаКонсервациюПомещений.ВернутьАктуальныеПомещенияДляОтветственногоЗаКонсервацию(_ФЛ);
		
		Для Каждого _Помещение Из _МассивПомещения Цикл
			Если (_Массив.Найти(_Помещение) = Неопределено) Тогда
				_Массив.Добавить(_Помещение);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат _Массив;
	
КонецФункции

Функция ВернутьАктуальныеДанныеПоОтветственнымИЗакрепленнымПомещениям() Экспорт 
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.Помещение КАК Помещение,
	               |	гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.ОтветственныйЗаКонсервациюПомещения КАК ОтветственныйЗаКонсервациюПомещения
	               |ИЗ
	               |	РегистрСведений.гхб_ОтветственныеЗаКонсервациюПомещений.СрезПоследних КАК гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних
	               |ГДЕ
	               |	НЕ гхб_ОтветственныеЗаКонсервациюПомещенийСрезПоследних.Закрыть
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОтветственныйЗаКонсервациюПомещения,
	               |	Помещение
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли