
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает массив структур, содержащий актуальные данные по проезду на локацию
// Параметры:
//		Локация - справочник гхб_Помещения
//		Сотрудник - справочник гхб_ФизическиеЛица, гхб_ПрочиеФизическиеЛица
//
Функция ВернутьАктуальныеДанныеДляСотрудника(_Сотрудник, _Локация) Экспорт
	
	_Массив = Новый Массив();
	
	Если ЗначениеЗаполнено(_Сотрудник) И ЗначениеЗаполнено(_Локация) Тогда
	
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.Локация КАК Локация,
		               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.ФИО КАК ФИО,
		               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.Автомобиль КАК Автомобиль,
		               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.ВидПропуска КАК ВидПропуска,
		               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.ДатаНачалаДействия КАК ДатаНачалаДействия,
		               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.ДатаОкончанияДействия КАК ДатаОкончанияДействия
		               |ИЗ
		               |	РегистрСведений.гхб_ПраваЗаездаНаТерриториюЛокации.СрезПоследних(
		               |			,
		               |			Локация = &Локация
		               |				И ФИО = &ФИО) КАК гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних
		               |ГДЕ
		               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.ДатаНачалаДействия <= &ТекущаяДата
		               |	И (гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.ДатаОкончанияДействия >= &ТекущаяДата
		               |			ИЛИ гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1))
		               |	И гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.ТипОперации = ЗНАЧЕНИЕ(Перечисление.гхб_ТипОперацииЗакреплениеОткреплениеПарковочногоМеста.Закрепление)";
		
		Запрос.УстановитьПараметр("Локация", _Локация);
		Запрос.УстановитьПараметр("ФИО", _Сотрудник);
		Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			_Массив.Добавить(Новый Структура("Локация, ФИО, Автомобиль, ВидПропуска, ДатаНачалаДействия, ДатаОкончанияДействия",
				Выборка.Локация, Выборка.ФИО, Выборка.Автомобиль, Выборка.ВидПропуска, Выборка.ДатаНачалаДействия, Выборка.ДатаОкончанияДействия));
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат _Массив;
	
КонецФункции

// Возвращает массив автомобилей, с которыми существовала связка сотрудника
// Параметры:
//		Сотрудник - справочник гхб_ФизическиеЛица, гхб_ПрочиеФизическиеЛица
//
Функция ВернутьДоступныеАвтомобилиДляСотрудника(_Сотрудник) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.Автомобиль КАК Автомобиль
	               |ИЗ
	               |	РегистрСведений.гхб_ПраваЗаездаНаТерриториюЛокации.СрезПоследних(, ФИО = &ФИО) КАК гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	гхб_ПраваЗаездаНаТерриториюЛокацииСрезПоследних.Автомобиль
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Автомобиль
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("ФИО", _Сотрудник);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Автомобиль");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли