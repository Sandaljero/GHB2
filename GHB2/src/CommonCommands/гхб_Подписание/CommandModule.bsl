
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКомандыДокумент(ПараметрКоманды) Тогда
	
		Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") Тогда 
					
			ОткрытьФормуПодписания = Истина;
			
			Если ПараметрыВыполненияКоманды.Источник.Модифицированность Тогда
				
				Ответ = Вопрос(НСтр("ru='Перед подписанием необходимо записать документ.
				|Продолжить с записью?'"), РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
				
				Если Ответ = КодВозвратаДиалога.Да Тогда
					
					_ДокументПроведен = ДокументПроведен(ПараметрКоманды);
					
					Если _ДокументПроведен Тогда
						РежимЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
					Иначе
						РежимЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись);
					КонецЕсли;
					
					ОткрытьФормуПодписания = ПараметрыВыполненияКоманды.Источник.Записать(РежимЗаписи);
					
				Иначе	
					
					ОткрытьФормуПодписания = Ложь;
					
				КонецЕсли;			
					
			КонецЕсли;		
				
			Если ОткрытьФормуПодписания Тогда
				
				ПараметрыОткрытия = Новый Структура;
				ПараметрыОткрытия.Вставить("Док", ПараметрКоманды);
				
				ОткрытьФорму("БизнесПроцесс.Подписание.Форма.ФормаМаршрутаУправляемая", ПараметрыОткрытия, 
					ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно);	
					
			Иначе		
				
				ПараметрыВыполненияКоманды.Источник.Активизировать();
				
			КонецЕсли;
					
		Иначе
				
			ПоказатьПредупреждение(, "Для команды подписания источником должна быть управляемая форма.", 60);
				
		КонецЕсли;
		
	Иначе
		
		ПоказатьПредупреждение(, "Для команды подписания параметром должен быть документ.", 60);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ДокументПроведен(_Документ)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(_Документ, "Проведен");
	
КонецФункции

&НаСервере
Функция ПараметрКомандыДокумент(ПараметрКоманды) 
	
	МетаданныеКоманды = Метаданные.НайтиПоТипу(ТипЗнч(ПараметрКоманды));
	
	Если МетаданныеКоманды = Неопределено Тогда
		
		Возврат Ложь;
		
	Иначе
		
		Возврат Метаданные.Документы.Содержит(МетаданныеКоманды);
		
	КонецЕсли;
	
КонецФункции