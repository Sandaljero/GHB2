
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Ищет элемент справочника "Автомобили" с указанным ГосНомером
// Параметры:
// 	   _ГосударственныйНомер - гос. номер автомобиля, по которому идет поиск
//	   _АвтомобильИскючение - ссылка на автомобиль, который не нужно учитывать при поиске (если мы в форме элемента какого-то авто)
// Возвражаемое значение:
//	   Ссылка на найденый автомобиль или же пустая ссылка если не нашли
//
Функция ВернутьАвтомобильПоГосударственномуНомеру(_ГосударственныйНомер, _АвтомобильИскючение = Неопределено) Экспорт 
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	гхб_Автомобили.Наименование КАК Наименование,
	               |	гхб_Автомобили.Код КАК Код,
	               |	гхб_Автомобили.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.гхб_Автомобили КАК гхб_Автомобили
	               |ГДЕ
	               |	гхб_Автомобили.Ссылка <> &Ссылка
	               |	И гхб_Автомобили.ГосударственныйНомер = &ГосударственныйНомер
	               |	И НЕ гхб_Автомобили.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Ссылка", _АвтомобильИскючение);
	Запрос.УстановитьПараметр("ГосударственныйНомер", ВРег(_ГосударственныйНомер));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ?(Выборка.Следующий(), Выборка.Ссылка, Справочники.гхб_Автомобили.ПустаяСсылка());
	
КонецФункции

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ФильтрПоТипуАвтомобиля") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СтрокаПоиска", ?(Параметры.СтрокаПоиска = Неопределено, "", Параметры.СтрокаПоиска) + "%");
		Запрос.УстановитьПараметр("ТипАвтомобиля", Параметры.ФильтрПоТипуАвтомобиля);
		
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 50
		               |	гхб_Автомобили.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.гхб_Автомобили КАК гхб_Автомобили
		               |ГДЕ
		               |	НЕ гхб_Автомобили.ПометкаУдаления
		               |	И НЕ гхб_Автомобили.ЭтоГруппа
		               |	И гхб_Автомобили.Наименование ПОДОБНО &СтрокаПоиска
		               |	И гхб_Автомобили.ТипАвтомобиля = &ТипАвтомобиля
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	гхб_Автомобили.Наименование";
		
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли