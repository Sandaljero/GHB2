
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВернутьДанныеПомещения(_Ссылка, _ДобавитьПоляНовые = Ложь) Экспорт 
	
	_Структура = Новый Структура();
	_Структура.Вставить("Родитель", Справочники.гхб_Помещения.ПустаяСсылка());
	_Структура.Вставить("Наименование", "");
	_Структура.Вставить("Руководитель", Справочники.гхб_ФизическиеЛица.ПустаяСсылка());
	_Структура.Вставить("ИспользоватьДляЗакрепленияПарковочныхМест", Ложь);
	_Структура.Вставить("ОтветственныйЗаЛокацию", Справочники.гхб_ФизическиеЛица.ПустаяСсылка());
	_Структура.Вставить("Локация", Справочники.гхб_Помещения.ПустаяСсылка());
	_Структура.Вставить("Корпус", Справочники.гхб_Помещения.ПустаяСсылка());
	_Структура.Вставить("ЭлементСтроения", Справочники.гхб_Помещения.ПустаяСсылка());
	_Структура.Вставить("Помещение", Справочники.гхб_Помещения.ПустаяСсылка());
	_Структура.Вставить("НомерПомещения", 0);
	_Структура.Вставить("ДробнаяЧасть", 0);
	_Структура.Вставить("Площадь", 0);
	_Структура.Вставить("ТипПомещения", Справочники.гхб_ТипыПомещений.ПустаяСсылка());
	_Структура.Вставить("ПлановоеКоличествоРабочихМест", 0);
	_Структура.Вставить("НетРабочихМест", Ложь);
	_Структура.Вставить("УточняющаяИнформация", "");
	_Структура.Вставить("НомерНаГенеральномПлане", "");
	_Структура.Вставить("Закрыть", Ложь);
	_Структура.Вставить("УИДСинхронизации", "");
	_Структура.Вставить("ЭтоПомещение", Ложь);
	_Структура.Вставить("ДоступВПомещениеПоКлючу", Ложь);
	_Структура.Вставить("НаличиеОхраннойСигнализации", Ложь);
	_Структура.Вставить("ВнутреннийНомерКТ", "");
	_Структура.Вставить("НаличиеКТ", Ложь);
	_Структура.Вставить("СогласованиеДоступаСВысшимРуководителем", Ложь);
	_Структура.Вставить("ОбщегоПользования", Ложь);
	
	Если _ДобавитьПоляНовые Тогда
		_Структура.Вставить("НаименованиеНовое", "");
		_Структура.Вставить("РуководительНовый", Справочники.гхб_ФизическиеЛица.ПустаяСсылка());
		_Структура.Вставить("ИспользоватьДляЗакрепленияПарковочныхМестНовое", Ложь);
		_Структура.Вставить("ЛокацияНовая", Справочники.гхб_Помещения.ПустаяСсылка());
		_Структура.Вставить("КорпусНовый", Справочники.гхб_Помещения.ПустаяСсылка());
		_Структура.Вставить("ЭлементСтроенияНовый", Справочники.гхб_Помещения.ПустаяСсылка());
		_Структура.Вставить("НомерПомещенияНовый", 0);
		_Структура.Вставить("ДробнаяЧастьНовая", 0);
		_Структура.Вставить("ПлощадьНовая", 0);
		_Структура.Вставить("ТипПомещенияНовый", Справочники.гхб_ТипыПомещений.ПустаяСсылка());
		_Структура.Вставить("ПлановоеКоличествоРабочихМестНовое", 0);
		_Структура.Вставить("НетРабочихМестНовое", Ложь);
		_Структура.Вставить("УточняющаяИнформацияНовая", "");
		_Структура.Вставить("НомерНаГенеральномПланеНовый", "");
		_Структура.Вставить("ЗакрытьНовое", Ложь);
		_Структура.Вставить("ДоступВПомещениеПоКлючуНовый", Ложь);
		_Структура.Вставить("НаличиеОхраннойСигнализацииНовое", Ложь);
		_Структура.Вставить("ВнутреннийНомерКТНовый", "");
		_Структура.Вставить("НаличиеКТНовое", Ложь);
		_Структура.Вставить("СогласованиеДоступаСВысшимРуководителемНовое", Ложь);
		_Структура.Вставить("ОбщегоПользованияНовый", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(_Ссылка) Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = ВернутьТекстЗапросаСвойстваПомещений(Истина);
		
		Запрос.УстановитьПараметр("Помещение", _Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(_Структура, Выборка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат _Структура;
	
КонецФункции

Функция ВернутьТекстЗапросаСвойстваПомещений(_ЕдиничнаяСсылка = Истина) Экспорт
	
	_Текст  = "ВЫБРАТЬ
           |	гхб_СвойстваПомещенийСрезПоследних.Помещение КАК Помещение,
           |	гхб_СвойстваПомещенийСрезПоследних.Наименование КАК Наименование,
           |	гхб_СвойстваПомещенийСрезПоследних.Наименование КАК НаименованиеНовое,
           |	гхб_СвойстваПомещенийСрезПоследних.Локация КАК Локация,
           |	гхб_СвойстваПомещенийСрезПоследних.Локация КАК ЛокацияНовая,
           |	гхб_СвойстваПомещенийСрезПоследних.Корпус КАК Корпус,
           |	гхб_СвойстваПомещенийСрезПоследних.Корпус КАК КорпусНовый,
           |	гхб_СвойстваПомещенийСрезПоследних.ЭлементСтроения КАК ЭлементСтроения,
           |	гхб_СвойстваПомещенийСрезПоследних.ЭлементСтроения КАК ЭлементСтроенияНовый,
           |	ЕСТЬNULL(гхб_РуководителиЛокацийСрезПоследних.Руководитель, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК Руководитель,
           |	ЕСТЬNULL(гхб_РуководителиЛокацийСрезПоследних.Руководитель, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК РуководительНовый,
           |	ЕСТЬNULL(гхб_ИспользованиеЛокацийДляЗакрепленияПарковочныхМестСрезПоследних.ИспользоватьДляЗакрепленияПарковочныхМест, ЛОЖЬ) КАК ИспользоватьДляЗакрепленияПарковочныхМест,
           |	ЕСТЬNULL(гхб_ИспользованиеЛокацийДляЗакрепленияПарковочныхМестСрезПоследних.ИспользоватьДляЗакрепленияПарковочныхМест, ЛОЖЬ) КАК ИспользоватьДляЗакрепленияПарковочныхМестНовое,
           |	ЕСТЬNULL(гхб_ОтветственныеЗаЛокацииСрезПоследних.ОтветственныйЗаЛокацию, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)) КАК ОтветственныйЗаЛокацию,
           |	гхб_СвойстваПомещенийСрезПоследних.Закрыть КАК Закрыть,
           |	гхб_СвойстваПомещенийСрезПоследних.Закрыть КАК ЗакрытьНовое,
           |	гхб_СвойстваПомещенийСрезПоследних.НомерПомещения КАК НомерПомещения,
           |	гхб_СвойстваПомещенийСрезПоследних.НомерПомещения КАК НомерПомещенияНовый,
           |	гхб_СвойстваПомещенийСрезПоследних.ДробнаяЧасть КАК ДробнаяЧасть,
           |	гхб_СвойстваПомещенийСрезПоследних.ДробнаяЧасть КАК ДробнаяЧастьНовая,
           |	ЕСТЬNULL(гхб_ПлощадиПомещений.Площадь, гхб_СвойстваПомещенийСрезПоследних.Площадь) КАК Площадь,
           |	ЕСТЬNULL(гхб_ПлощадиПомещений.Площадь, гхб_СвойстваПомещенийСрезПоследних.Площадь) КАК ПлощадьНовая,
           |	гхб_СвойстваПомещенийСрезПоследних.ТипПомещения КАК ТипПомещения,
           |	гхб_СвойстваПомещенийСрезПоследних.ТипПомещения КАК ТипПомещенияНовый,
           |	гхб_СвойстваПомещенийСрезПоследних.ПлановоеКоличествоРабочихМест КАК ПлановоеКоличествоРабочихМест,
           |	гхб_СвойстваПомещенийСрезПоследних.ПлановоеКоличествоРабочихМест КАК ПлановоеКоличествоРабочихМестНовое,
           |	гхб_СвойстваПомещенийСрезПоследних.НетРабочихМест КАК НетРабочихМест,
           |	гхб_СвойстваПомещенийСрезПоследних.НетРабочихМест КАК НетРабочихМестНовое,
           |	гхб_СвойстваПомещенийСрезПоследних.УточняющаяИнформация КАК УточняющаяИнформация,
           |	гхб_СвойстваПомещенийСрезПоследних.УточняющаяИнформация КАК УточняющаяИнформацияНовая,
           |	гхб_СвойстваПомещенийСрезПоследних.НомерНаГенеральномПлане КАК НомерНаГенеральномПлане,
           |	гхб_СвойстваПомещенийСрезПоследних.НомерНаГенеральномПлане КАК НомерНаГенеральномПланеНовый,
           |	гхб_СвойстваПомещенийСрезПоследних.ДоступВПомещениеПоКлючу КАК ДоступВПомещениеПоКлючу,
           |	гхб_СвойстваПомещенийСрезПоследних.ДоступВПомещениеПоКлючу КАК ДоступВПомещениеПоКлючуНовый,
           |	гхб_СвойстваПомещенийСрезПоследних.НаличиеОхраннойСигнализации КАК НаличиеОхраннойСигнализации,
           |	гхб_СвойстваПомещенийСрезПоследних.НаличиеОхраннойСигнализации КАК НаличиеОхраннойСигнализацииНовое,
           |	гхб_СвойстваПомещенийСрезПоследних.ВнутреннийНомерКТ КАК ВнутреннийНомерКТ,
           |	гхб_СвойстваПомещенийСрезПоследних.ВнутреннийНомерКТ КАК ВнутреннийНомерКТНовый,
           |	гхб_СвойстваПомещенийСрезПоследних.НаличиеКТ КАК НаличиеКТ,
           |	гхб_СвойстваПомещенийСрезПоследних.НаличиеКТ КАК НаличиеКТНовое,
           |	гхб_СвойстваПомещенийСрезПоследних.СогласованиеДоступаСВысшимРуководителем КАК СогласованиеДоступаСВысшимРуководителем,
           |	гхб_СвойстваПомещенийСрезПоследних.СогласованиеДоступаСВысшимРуководителем КАК СогласованиеДоступаСВысшимРуководителемНовое,
           |	гхб_СвойстваПомещенийСрезПоследних.ОбщегоПользования КАК ОбщегоПользования,
           |	гхб_СвойстваПомещенийСрезПоследних.ОбщегоПользования КАК ОбщегоПользованияНовый,
           |	гхб_СвойстваПомещенийСрезПоследних.Помещение.УИДСинхронизации КАК УИДСинхронизации,
           |	гхб_СвойстваПомещенийСрезПоследних.Помещение.Родитель КАК Родитель,
           |	гхб_СвойстваПомещенийСрезПоследних.ТипПомещения.ЭтоПомещение КАК ЭтоПомещение
           |ИЗ
           |	РегистрСведений.гхб_СвойстваПомещений.СрезПоследних(, Помещение " + ?(_ЕдиничнаяСсылка, "= &Помещение", "В (&Помещение)") + ") КАК гхб_СвойстваПомещенийСрезПоследних
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гхб_РуководителиЛокаций.СрезПоследних(, Локация " + ?(_ЕдиничнаяСсылка, "= &Помещение", "В (&Помещение)") + ") КАК гхб_РуководителиЛокацийСрезПоследних
           |		ПО гхб_СвойстваПомещенийСрезПоследних.Помещение = гхб_РуководителиЛокацийСрезПоследних.Локация
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гхб_ИспользованиеЛокацийДляЗакрепленияПарковочныхМест.СрезПоследних(, Локация " + ?(_ЕдиничнаяСсылка, "= &Помещение", "В (&Помещение)") + ") КАК гхб_ИспользованиеЛокацийДляЗакрепленияПарковочныхМестСрезПоследних
           |		ПО гхб_СвойстваПомещенийСрезПоследних.Помещение = гхб_ИспользованиеЛокацийДляЗакрепленияПарковочныхМестСрезПоследних.Локация
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гхб_ОтветственныеЗаЛокации.СрезПоследних(, Локация " + ?(_ЕдиничнаяСсылка, "= &Помещение", "В (&Помещение)") + ") КАК гхб_ОтветственныеЗаЛокацииСрезПоследних
           |		ПО гхб_СвойстваПомещенийСрезПоследних.Помещение = гхб_ОтветственныеЗаЛокацииСрезПоследних.Локация
           |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гхб_ПлощадиПомещений КАК гхб_ПлощадиПомещений
           |		ПО гхб_СвойстваПомещенийСрезПоследних.Помещение = гхб_ПлощадиПомещений.Помещение";
	
	Возврат _Текст;
	
КонецФункции

Функция ВернутьПолноеНаименование(_Ссылка) Экспорт
	
	_Результат = "";
	
	Если ЗначениеЗаполнено(_Ссылка) Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ
		               |	гхб_СвойстваПомещенийСрезПоследних.НаименованиеПолное КАК НаименованиеПолное
		               |ИЗ
		               |	РегистрСведений.гхб_СвойстваПомещений.СрезПоследних(, Помещение = &Помещение) КАК гхб_СвойстваПомещенийСрезПоследних";
		
		Запрос.УстановитьПараметр("Помещение", _Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		
		_Результат = ?(Выборка.Следующий(), Выборка.НаименованиеПолное, "");
		
	КонецЕсли;
	
	Возврат _Результат;
	
КонецФункции

Функция ВернутьНаименованиеДляВыгрузкиВАД(_Ссылка) Экспорт
	
	_Результат = ВернутьПолноеНаименование(_Ссылка);
	
	_Позиция = СтрНайти(_Результат, "(");
	
	Если НЕ (_Позиция = 0) Тогда
		_Результат = СокрЛП(Лев(_Результат, _Позиция - 1));
	КонецЕсли;
	
	Возврат _Результат;
	
КонецФункции

Функция ВернутьАктивныеПомещенияПоТипу(_Тип) Экспорт 
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	гхб_СвойстваПомещенийСрезПоследних.Помещение КАК Помещение
	               |ИЗ
	               |	РегистрСведений.гхб_СвойстваПомещений.СрезПоследних КАК гхб_СвойстваПомещенийСрезПоследних
	               |ГДЕ
	               |	НЕ гхб_СвойстваПомещенийСрезПоследних.Закрыть
	               |	И гхб_СвойстваПомещенийСрезПоследних.ТипПомещения = &ТипПомещения";
	
	Запрос.УстановитьПараметр("ТипПомещения", _Тип);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Помещение");
	
КонецФункции

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	_УсловиеКонсервации = "";
	
	Если Параметры.Свойство("КонсервацияПомещений") Тогда
		
		_УсловиеКонсервации = " И " + ?(Параметры.КонсервацияПомещений, "", "НЕ ") + "гхб_СвойстваПомещенийСрезПоследних.ТипПомещения.ИспользоватьДляКонсервацииПомещений"
		
	КонецЕсли;
	
	Если Параметры.Свойство("ФильтрПоПомещениям") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СтрокаПоиска", ?(Параметры.СтрокаПоиска = Неопределено, "", Параметры.СтрокаПоиска) + "%");
		Запрос.УстановитьПараметр("СписокЛокаций", Параметры.ФильтрПоПомещениям);
		
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 50
		               |	гхб_Помещения.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.гхб_Помещения КАК гхб_Помещения
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.гхб_СвойстваПомещений.СрезПоследних КАК гхб_СвойстваПомещенийСрезПоследних
		               |		ПО гхб_Помещения.Ссылка = гхб_СвойстваПомещенийСрезПоследних.Помещение
		               |ГДЕ
		               |	гхб_Помещения.Ссылка В ИЕРАРХИИ(&СписокЛокаций)
		               |	И НЕ гхб_Помещения.ПометкаУдаления
		               |	И НЕ гхб_Помещения.ЭтоГруппа
		               |	И гхб_Помещения.Наименование ПОДОБНО &СтрокаПоиска
		               |	И НЕ гхб_СвойстваПомещенийСрезПоследних.Закрыть
					   |	" + _УсловиеКонсервации + "
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	гхб_Помещения.Наименование";
		
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
	КонецЕсли;
	
КонецПроцедуры

Функция ВернутьПомещениеПоИдентификатору(_УИД) Экспорт 
	
	Если ЗначениеЗаполнено(_УИД) Тогда
	
		_Помещение = Справочники.гхб_Помещения.НайтиПоРеквизиту("УИДСинхронизации", _УИД);
		
		Если НЕ ЗначениеЗаполнено(_Помещение) Тогда
			_Помещение = Справочники.гхб_Помещения.ПолучитьСсылку(Новый УникальныйИдентификатор(_УИД));
		КонецЕсли;
		
	Иначе 
		
		_Помещение = Справочники.гхб_Помещения.ПустаяСсылка();
		
	КонецЕсли;
	
	Возврат _Помещение;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли