
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	сп = Элементы.ТипДокумента.СписокВыбора;	
	
	_С = ВернутьСписокДокументов(); 
	
	Для Каждого _Док Из _С Цикл
		сп.Добавить(_Док.Значение, _Док.Представление);
	КонецЦикла;
	
	сп.СортироватьПоПредставлению();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаименованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ПустаяСтрока(ВыбранноеЗначение) Тогда
		Объект.Наименование = "";
		Объект.ТипДокумента = "";
	Иначе
		НС = Элементы.ТипДокумента.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		
		Если НЕ (НС = Неопределено) Тогда
			Объект.Наименование = НС.Представление;
			Объект.ТипДокумента = НС.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Объект.ИспользуемыеТЧ.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуемыеТЧСинонимТЧНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	_С = ВернутьСписокТЧДокумента(Объект.ТипДокумента);
	
	Для Каждого ТЧ Из _С Цикл
		Элемент.СписокВыбора.Добавить(ТЧ.Значение, ТЧ.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуемыеТЧСинонимТЧОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТД = Элементы.ИспользуемыеТЧ.ТекущиеДанные;
	
	Если ПустаяСтрока(ВыбранноеЗначение) Тогда
		ТД.СинонимТЧ = "";
		ТД.ИмяТЧ = "";
	Иначе
		НС = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		
		Если НЕ (НС = Неопределено) Тогда
			ТД.СинонимТЧ = НС.Представление;
			ТД.ИмяТЧ = НС.Значение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервереБезКонтекста
Функция ВернутьСписокДокументов()
	
	_СП = Новый СписокЗначений();
	
	Для Каждого Док Из Метаданные.Документы Цикл 
		_СП.Добавить(Док.Имя, Док.Синоним)
	КонецЦикла;
	
	Возврат _СП;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьСписокТЧДокумента(_ТипДокумента)
	
	_СпТЧ = Новый СписокЗначений();
	
	Док = Метаданные.Документы.Найти(_ТипДокумента);
	
	Если НЕ (Док = Неопределено) Тогда
		Для Каждого ТЧ Из Док.ТабличныеЧасти Цикл
			_СпТЧ.Добавить(ТЧ.Имя, ТЧ.Синоним);
		КонецЦикла;
	КонецЕсли;
	
	Возврат _СпТЧ;
	
КонецФункции

#КонецОбласти
