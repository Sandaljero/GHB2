
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	гхб_СотрудникиБазХолдинга.Ссылка КАК Сотрудник,
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.Дивизион КАК Дивизион,
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.Должность КАК Должность,
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.ЦФО КАК ЦФО,
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.БазаХолдинга КАК БазаХолдинга
	|ИЗ
	|	РегистрСведений.гхб_РаботникиОрганизацийБазХолдинга.СрезПоследних КАК гхб_РаботникиОрганизацийБазХолдингаСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гхб_СотрудникиБазХолдинга КАК гхб_СотрудникиБазХолдинга
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гхб_ФизическиеЛицаБазХолдинга КАК гхб_ФизическиеЛицаБазХолдинга
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гхб_ФизическиеЛица КАК гхб_ФизическиеЛица
	|				ПО гхб_ФизическиеЛицаБазХолдинга.ФизЛицоТекущейБазы = гхб_ФизическиеЛица.Ссылка
	|					И (гхб_ФизическиеЛица.Ссылка = &ФизЛицо)
	|			ПО гхб_СотрудникиБазХолдинга.ФизЛицоБазыХолдинга = гхб_ФизическиеЛицаБазХолдинга.Ссылка
	|				И (гхб_СотрудникиБазХолдинга.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.гхб_ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы))
	|		ПО гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.Сотрудник = гхб_СотрудникиБазХолдинга.Ссылка
	|ГДЕ
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.гхб_ПричиныИзмененияСостояния.Увольнение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	гхб_СотрудникиБазХолдинга.Ссылка КАК Сотрудник,
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.Дивизион КАК Дивизион,
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.Должность КАК Должность,
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.ЦФО КАК ЦФО,
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.БазаХолдинга КАК БазаХолдинга
	|ИЗ
	|	РегистрСведений.гхб_РаботникиОрганизацийБазХолдинга.СрезПоследних КАК гхб_РаботникиОрганизацийБазХолдингаСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гхб_СотрудникиБазХолдинга КАК гхб_СотрудникиБазХолдинга
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гхб_ФизическиеЛицаБазХолдинга КАК гхб_ФизическиеЛицаБазХолдинга
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гхб_ФизическиеЛица КАК гхб_ФизическиеЛица
	|				ПО гхб_ФизическиеЛицаБазХолдинга.ФизЛицоТекущейБазы = гхб_ФизическиеЛица.Ссылка
	|					И (гхб_ФизическиеЛица.Ссылка = &ФизЛицо)
	|			ПО гхб_СотрудникиБазХолдинга.ФизЛицоБазыХолдинга = гхб_ФизическиеЛицаБазХолдинга.Ссылка
	|				И (гхб_СотрудникиБазХолдинга.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.гхб_ВидыЗанятостиВОрганизации.Совместительство))
	|		ПО гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.Сотрудник = гхб_СотрудникиБазХолдинга.Ссылка
	|ГДЕ
	|	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.гхб_ПричиныИзмененияСостояния.Увольнение)";
	
	Запрос.УстановитьПараметр("ФизЛицо", Параметры.ФизЛицо);
	резЗапросПакет = Запрос.ВыполнитьПакет();
	
	Выборка = резЗапросПакет[0].Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		новСтр = Данные.Добавить();
		ЗаполнитьЗначенияСвойств(новСтр, Выборка);
	
	КонецЦикла;
	
	Выборка = резЗапросПакет[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		мНайденное = Данные.НайтиСтроки(Новый Структура("БазаХолдинга", Выборка.БазаХолдинга));
		
		Если мНайденное.Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		новСтр = Данные.Добавить();
		ЗаполнитьЗначенияСвойств(новСтр, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ДанныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДанныеПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДанныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	текДанные = Элементы.Данные.ТекущиеДанные;
	Закрыть(Новый Структура("БазаХолдинга", текДанные.БазаХолдинга));
	
КонецПроцедуры

#КонецОбласти

