
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(гхб_Ответственный) Тогда
		гхб_Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	гхб_Ответственный = Пользователи.ТекущийПользователь();	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Лимиты.Количество() = 0 Тогда
		гхб_Комментарий = "Пустой документ!";
	КонецЕсли;
	
	Если ЗапрещеноИзменение Тогда
		ТекстСообщения = НСтр("ru = 'Нельзя менять изначальные документы остатков из баз РИБ!'; 
			         		  |uk = 'Не можна змінювати початкові документи залишків з баз РІБ!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УИДИзБазыХолдинга) Тогда
		
		Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			ТекстСообщения = НСтр("ru = 'Невозможно отменять проведение у документа, который уже мигрировал в РИБ!'; 
			         		  	  |uk = 'Неможливо скасовувати проведення у документа, який вже мігрував у РІБ!'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			Возврат;													
		КонецЕсли;
		
		Если НЕ ДополнительныеСвойства.Свойство("ЭтоМиграция") Тогда
			ТекстСообщения = НСтр("ru = 'Модификация документа возможна только через кнопку ""Миграция в базу РИБ""!'; 
			         		 	  |uk = 'Неможливо скасовувати проведення у документу, який вже мігрував у РІБ!'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецЕсли;											
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаНаНаличиеПовторенииЗаписиПоТелефону(Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СтруктураПроведения = Документы.гхб_ЛимитНаМобильнуюСвязь.ПодготовитьТЗПроведения(Ссылка);
	Документы.гхб_ЛимитНаМобильнуюСвязь.СформироватьДвижения(СтруктураПроведения, Движения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаНаНаличиеПовторенииЗаписиПоТелефону(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гхб_ЛимитНаМобильнуюСвязь.НомерТелефона КАК НомерТелефона,
		|	гхб_ЛимитНаМобильнуюСвязь.ОтветственныйБазыХолдинга КАК Ответственный,
		|	гхб_ЛимитНаМобильнуюСвязьСвязанный.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.гхб_ЛимитНаМобильнуюСвязь.Лимиты КАК гхб_ЛимитНаМобильнуюСвязь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гхб_ЛимитНаМобильнуюСвязь.Лимиты КАК гхб_ЛимитНаМобильнуюСвязьСвязанный
		|		ПО гхб_ЛимитНаМобильнуюСвязь.НомерТелефона = гхб_ЛимитНаМобильнуюСвязьСвязанный.НомерТелефона
		|			И гхб_ЛимитНаМобильнуюСвязь.ОтветственныйБазыХолдинга = гхб_ЛимитНаМобильнуюСвязьСвязанный.ОтветственныйБазыХолдинга
		|			И гхб_ЛимитНаМобильнуюСвязь.Ссылка <> гхб_ЛимитНаМобильнуюСвязьСвязанный.Ссылка
		|			И гхб_ЛимитНаМобильнуюСвязь.Ссылка.БазаХолдинга = гхб_ЛимитНаМобильнуюСвязьСвязанный.Ссылка.БазаХолдинга
		|			И (НЕ гхб_ЛимитНаМобильнуюСвязь.Ссылка.ПометкаУдаления)
		|			И (НАЧАЛОПЕРИОДА(гхб_ЛимитНаМобильнуюСвязь.Ссылка.ДатаНачалаДействия, МЕСЯЦ) = НАЧАЛОПЕРИОДА(гхб_ЛимитНаМобильнуюСвязьСвязанный.Ссылка.ДатаНачалаДействия, МЕСЯЦ))
		|ГДЕ
		|	гхб_ЛимитНаМобильнуюСвязь.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		ТЗПовторений = РезультатЗапроса.Выгрузить();
		
		ТекстСообщения = НСтр("ru = 'Невозможно провести документ! Есть задвоении записей по телефону и отвественному:'; 
			         		  |uk = 'Неможливо провести документ! Є задвоєння записів по телефону і відповідальному:'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		
		Для Каждого Стр Из ТЗПовторений Цикл
			
			ТекстСообщения = НСтр("ru = 'Телефон: %1 Ответственный: %2 Документ: %3'; 
			         		  	  |uk = 'Телефон: %1 Відповідальний: %2 Документ: %3'");
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения, Стр.НомерТелефона, Стр.Ответственный, Стр.Ссылка));
			
		КонецЦикла;
		
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#КонецЕсли
