
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ФормированиеДвижений

// Формирование структуры с параметрами проведения документа 
//
// Параметры:
//  ДокументСсылка - Документ "гхб_ЛимитНаМобильнуюСвязь"
//
// Возвращаемое значение:
//   СтруктураПроведения  - Струтура из 2 таблиц ТЗЛимиты и ТЗДополнительныеУслуги
//
Функция ПодготовитьТЗПроведения(ДокументСсылка) Экспорт
	
	СтруктураПроведения = Новый Структура();
	СтруктураПроведения.Вставить("ТЗЛимиты", СформироватьТЗЛимиты(ДокументСсылка));
	СтруктураПроведения.Вставить("ТЗДополнительныеУслуги", СформироватьТЗДополнительныеУслуги(ДокументСсылка));
	
	Возврат СтруктураПроведения;
	
КонецФункции

// Формирование движений по регистрам соответствий 
//
// Параметры:
//  СтруктураПроведения - Струтура из 2 таблиц ТЗЛимиты и ТЗДополнительныеУслуги
//  Движения - Движения документа "гхб_ЛимитНаМобильнуюСвязь"
//
Процедура СформироватьДвижения(СтруктураПроведения, Движения) Экспорт
	
	СформироватьДвиженияПоТЗ(Движения, СтруктураПроведения.ТЗЛимиты, "гхб_СоответствиеНомеровТелефоновФизЛицам");
	СформироватьДвиженияПоТЗ(Движения, СтруктураПроведения.ТЗДополнительныеУслуги, 
								"гхб_СоответствиеНомеровТелефоновДопУслугам");
	
	Движения.Записать();
	
КонецПроцедуры

#КонецОбласти

// Выполняется миграция данного документа в базу РИБ, которая указана в этом документе
// Сопоставление свойств для всех справочников через УИД
//
// Параметры:
//  ЛимитНаМобильнуюСвязь - Документ "гхб_ЛимитНаМобильнуюСвязь"
//
// Возвращаемое значение:
//   ОбменПрошел  - Булево. Признак, что обмен прошел удачно
//
Функция ОтправитьДокументВБазуРИБ(ЛимитНаМобильнуюСвязь) Экспорт

	РезультатЗапроса = ВернутьСвойстваДокументаДляЗагрузки(ЛимитНаМобильнуюСвязь);
	
	ТЗШапкаДокумента 	   = РезультатЗапроса[0].Выгрузить();
	ТЗЛимиты			   = РезультатЗапроса[1].Выгрузить();
	ТЗДополнительныеУслуги = РезультатЗапроса[3].Выгрузить();
	
	ОбменПрошел = Ложь;
			
	СтрШапки = ТЗШапкаДокумента[0]; 
			
	СтруктураПараметров = Новый Структура;
	
	// Шапка
	СтруктураПараметров.Вставить("УИДИзБазыГХБ2", Строка(СтрШапки.Ссылка.УникальныйИдентификатор()));   
	СтруктураПараметров.Вставить("УИДЛимитаНаМобильнуюСвязь", СтрШапки.УИДИзБазыХолдинга);
	СтруктураПараметров.Вставить("Дата", СтрШапки.Дата);
	СтруктураПараметров.Вставить("ДатаНачалаДействия", СтрШапки.ДатаНачалаДействия);
	СтруктураПараметров.Вставить("УИДОрганизация", СтрШапки.УИДОрганизация);
	СтруктураПараметров.Вставить("НаименованиеОрганизация", СтрШапки.НаименованиеОрганизация);
	СтруктураПараметров.Вставить("УчетМобСвязиВРазрезеЛицевыхСчетов", СтрШапки.УчетМобСвязиВРазрезеЛицевыхСчетов);
	СтруктураПараметров.Вставить("Комментарий", СтрШапки.Комментарий);
	
	// Лимиты
	МассивЛимиты = Новый Массив;
	
	Для каждого СтрЛимит Из ТЗЛимиты Цикл
	
		СтруктураСтроки = Новый Структура;
		СтруктураСтроки.Вставить("НаименованиеТелефон");
		СтруктураСтроки.Вставить("НаименованиеФизЛицо");
		СтруктураСтроки.Вставить("УИДФизЛицо");
		СтруктураСтроки.Вставить("ЛимитВнутренний");
		СтруктураСтроки.Вставить("ЛимитМеждународнойСвязи");
		СтруктураСтроки.Вставить("БезлимитныйВнутренний");
		СтруктураСтроки.Вставить("БезлимитнаяМеждународнаяСвязь");
		СтруктураСтроки.Вставить("НаименованиеЦеновойПакет");
		СтруктураСтроки.Вставить("НаименованиеОтветственный");
		СтруктураСтроки.Вставить("УИДОтветственный");
		СтруктураСтроки.Вставить("НомерЛицевогоСчета");
		
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, СтрЛимит);			
		
		УИДТелефон = ?(ЗначениеЗаполнено(СтрЛимит.СсылкаТелефон), 
						Строка(СтрЛимит.СсылкаТелефон.УникальныйИдентификатор()), "");
		СтруктураСтроки.Вставить("УИДТелефон", УИДТелефон);
		УИДЦеновойПакет = ?(ЗначениеЗаполнено(СтрЛимит.СсылкаЦеновойПакет), 
							Строка(СтрЛимит.СсылкаЦеновойПакет.УникальныйИдентификатор()), "");
		СтруктураСтроки.Вставить("УИДЦеновойПакет", УИДЦеновойПакет);
		СтруктураСтроки.Вставить("Использование", XMLСтрока(СтрЛимит.Использование));
		СтруктураСтроки.Вставить("ТипУстройства", XMLСтрока(СтрЛимит.ТипУстройства));
		
		МассивЛимиты.Добавить(СтруктураСтроки);
	
	КонецЦикла;
	
	СтруктураПараметров.Вставить("Лимиты", МассивЛимиты);
	
	// Дополнительные услуги
	МассивДополнительныеУслуги =  Новый Массив;
	
	Для каждого СтрДопУслуга Из ТЗДополнительныеУслуги Цикл
		
		СтруктураСтроки = Новый Структура;
		СтруктураСтроки.Вставить("НаименованиеТелефон");
		СтруктураСтроки.Вставить("НаименованиеДопУслуга");
		СтруктураСтроки.Вставить("Сумма");
		
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, СтрДопУслуга);
		
		УИДТелефон = ?(ЗначениеЗаполнено(СтрДопУслуга.СсылкаТелефон), 
						Строка(СтрДопУслуга.СсылкаТелефон.УникальныйИдентификатор()), "");
		СтруктураСтроки.Вставить("УИДТелефон", УИДТелефон);
	  	УИДДопУслуга = ?(ЗначениеЗаполнено(СтрДопУслуга.СсылкаДопУслуга), 
						 Строка(СтрДопУслуга.СсылкаДопУслуга.УникальныйИдентификатор()), "");
		СтруктураСтроки.Вставить("УИДДопУслуга", УИДДопУслуга);
		
		МассивДополнительныеУслуги.Добавить(СтруктураСтроки);
		
	КонецЦикла;
	
	СтруктураПараметров.Вставить("ДополнительныеУслуги", МассивДополнительныеУслуги);
	
	ОтветСервиса = JsonRPC.ВыполнитьПроцедуруJsonRPC(СтрШапки.ВнешнийИсточник, 
														"СоздатьИзменитьЛимитНаМобильнуюСвязь", 
														СтруктураПараметров);
														
	ТекстНеудачногоОбмена = НСтр("ru = 'Не удалось создать документ лимита по причине: '; 
								 |uk = 'Не вдалося створити документ ліміту по причині: '");
	Если НЕ ОтветСервиса.ОбменПрошел Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстНеудачногоОбмена + ОтветСервиса.ТекстОшибки + ". База - " 
												+ СтрШапки.БазаХолдинга);
	Иначе
		СтрокаРезультата = ОтветСервиса.Результат.result.Данные[0];
		Если НЕ ЗначениеЗаполнено(СтрокаРезультата.УИДЛимитаНаМобильнуюСвязь) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстНеудачногоОбмена + СтрокаРезультата.ОписаниеОшибки + ". База - " 
													+ СтрШапки.БазаХолдинга);
		Иначе
			
			Попытка
		
				УстановитьПривилегированныйРежим(Истина);
				
				ОбъектЛимит = ЛимитНаМобильнуюСвязь.ПолучитьОбъект();
				ОбъектЛимит.УИДИзБазыХолдинга = СтрокаРезультата.УИДЛимитаНаМобильнуюСвязь;
				ОбъектЛимит.ИзмененНоНеМигрировал = Ложь;
				ОбъектЛимит.ДополнительныеСвойства.Вставить("ЭтоМиграция", Истина);
				ОбъектЛимит.Записать(РежимЗаписиДокумента.Проведение);
				
				СтруктураЗаписи = Новый Структура;
				СтруктураЗаписи.Вставить("Период", гхб_ОбщегоНазначенияСервер.ВернутьВремяНаСервере());
				СтруктураЗаписи.Вставить("БазаХолдинга", СтрШапки.БазаХолдинга);
				СтруктураЗаписи.Вставить("Объект", СтрШапки.Ссылка);
				СтруктураЗаписи.Вставить("Тип", Перечисления.гхб_ТипыОбменовДляБазХолдинга.МиграцияОбъекта);
				СтруктураЗаписи.Вставить("Значение", СтрШапки.Ссылка);           
				СтруктураЗаписи.Вставить("Пользователь", Пользователи.ТекущийПользователь());
				
				РегистрыСведений.гхб_ИсторияОбменовСБазамиХолдинга.ЗаписатьСтрокуВРегистр(СтруктураЗаписи);
				
				УстановитьПривилегированныйРежим(Ложь);
				
				ТекстСообщения = НСтр("ru = 'Обмен прошел удачно!'; uk = 'Обмін пройшов вдало!'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				ОбменПрошел = Истина;
				
			Исключение
				
				ТекстСообщения = НСтр("ru = 'Не удалось записать УИД из базы РИБ и историю миграции!'; 
		         		  	  		  |uk = 'Не вдалося записати УІД з бази РІБ та історію міграції!'");		
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				
			КонецПопытки;	
				
		КонецЕсли;
	КонецЕсли;	
	
	Возврат ОбменПрошел;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВернутьСвойстваДокументаДляЗагрузки(ЛимитНаМобильнуюСвязь)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЛимитНаМобильнуюСвязь", ЛимитНаМобильнуюСвязь);
	
	СтруктураЗначений = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЛимитНаМобильнуюСвязь, 
																	"БазаХолдинга, ДатаНачалаДействия");
	Запрос.УстановитьПараметр("БазаХолдинга", СтруктураЗначений.БазаХолдинга);
	Запрос.УстановитьПараметр("ДатаНачалаДействия", СтруктураЗначений.ДатаНачалаДействия);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	гхб_ЛимитНаМобильнуюСвязь.Ссылка КАК Ссылка,
		|	гхб_ЛимитНаМобильнуюСвязь.Дата КАК Дата,
		|	гхб_ЛимитНаМобильнуюСвязь.УИДИзБазыХолдинга КАК УИДИзБазыХолдинга,
		|	гхб_ЛимитНаМобильнуюСвязь.БазаХолдинга КАК БазаХолдинга,
		|	гхб_ЛимитНаМобильнуюСвязь.ДатаНачалаДействия КАК ДатаНачалаДействия,
		|	гхб_ОрганизацииБазХолдинга.GUIDБазыХолдинга КАК УИДОрганизация,
		|	гхб_ОрганизацииБазХолдинга.Наименование КАК НаименованиеОрганизация,
		|	гхб_ЛимитНаМобильнуюСвязь.гхб_Комментарий КАК Комментарий,
		|	гхб_БазыХолдинга.ВнешнийИсточникПоУмолчанию КАК ВнешнийИсточник,
		|	гхб_БазыХолдинга.УчетМобСвязиВРазрезеЛицевыхСчетов КАК УчетМобСвязиВРазрезеЛицевыхСчетов
		|ИЗ
		|	Документ.гхб_ЛимитНаМобильнуюСвязь КАК гхб_ЛимитНаМобильнуюСвязь
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_ОрганизацииБазХолдинга КАК гхб_ОрганизацииБазХолдинга
		|		ПО гхб_ЛимитНаМобильнуюСвязь.Организация = гхб_ОрганизацииБазХолдинга.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_БазыХолдинга КАК гхб_БазыХолдинга
		|		ПО гхб_ЛимитНаМобильнуюСвязь.БазаХолдинга = гхб_БазыХолдинга.Ссылка
		|ГДЕ
		|	гхб_ЛимитНаМобильнуюСвязь.Ссылка = &ЛимитНаМобильнуюСвязь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	гхб_НомераТелефонов.Ссылка КАК СсылкаТелефон,
		|	гхб_НомераТелефонов.Наименование КАК НаименованиеТелефон,
		|	ВЫБОР
		|		КОГДА гхб_ЛимитНаМобильнуюСвязьЛимиты.ФизЛицоБазыХолдинга = ЗНАЧЕНИЕ(Справочник.гхб_ФизическиеЛицаБазХолдинга.ПустаяСсылка)
		|			ТОГДА """"
		|		КОГДА НЕ гхб_ЛимитНаМобильнуюСвязьЛимиты.ФизЛицоБазыХолдинга ССЫЛКА Справочник.гхб_ФизическиеЛицаБазХолдинга
		|			ТОГДА гхб_ЛимитНаМобильнуюСвязьЛимиты.ФизЛицоБазыХолдинга
		|		ИНАЧЕ гхб_ФизическиеЛицаБазХолдингаФизЛицо.Наименование
		|	КОНЕЦ КАК НаименованиеФизЛицо,
		|	ВЫБОР
		|		КОГДА НЕ гхб_ЛимитНаМобильнуюСвязьЛимиты.ФизЛицоБазыХолдинга ССЫЛКА Справочник.гхб_ФизическиеЛицаБазХолдинга
		|				ИЛИ гхб_ЛимитНаМобильнуюСвязьЛимиты.ФизЛицоБазыХолдинга = ЗНАЧЕНИЕ(Справочник.гхб_ФизическиеЛицаБазХолдинга.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ гхб_ФизическиеЛицаБазХолдингаФизЛицо.GUIDБазыХолдинга
		|	КОНЕЦ КАК УИДФизЛицо,
		|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ЛимитВнутренний КАК ЛимитВнутренний,
		|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ЛимитМеждународнойСвязи КАК ЛимитМеждународнойСвязи,
		|	гхб_ЛимитНаМобильнуюСвязьЛимиты.Использование КАК Использование,
		|	гхб_ЛимитНаМобильнуюСвязьЛимиты.БезлимитныйВнутренний КАК БезлимитныйВнутренний,
		|	гхб_ЛимитНаМобильнуюСвязьЛимиты.БезлимитнаяМеждународнаяСвязь КАК БезлимитнаяМеждународнаяСвязь,
		|	гхб_ЦеновыеПакетыМобильнойСвязи.Ссылка КАК СсылкаЦеновойПакет,
		|	ВЫБОР
		|		КОГДА гхб_ЛимитНаМобильнуюСвязьЛимиты.ЦеновойПакет <> ЗНАЧЕНИЕ(Справочник.гхб_ЦеновыеПакетыМобильнойСвязи.ПустаяСсылка)
		|			ТОГДА гхб_ЦеновыеПакетыМобильнойСвязи.Наименование
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НаименованиеЦеновойПакет,
		|	ВЫБОР
		|		КОГДА гхб_ЛимитНаМобильнуюСвязьЛимиты.ОтветственныйБазыХолдинга = ЗНАЧЕНИЕ(Справочник.гхб_ФизическиеЛицаБазХолдинга.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ гхб_ФизическиеЛицаБазХолдингаОтветственный.Наименование
		|	КОНЕЦ КАК НаименованиеОтветственный,
		|	ВЫБОР
		|		КОГДА гхб_ЛимитНаМобильнуюСвязьЛимиты.ОтветственныйБазыХолдинга = ЗНАЧЕНИЕ(Справочник.гхб_ФизическиеЛицаБазХолдинга.ПустаяСсылка)
		|			ТОГДА """"
		|		ИНАЧЕ гхб_ФизическиеЛицаБазХолдингаОтветственный.GUIDБазыХолдинга
		|	КОНЕЦ КАК УИДОтветственный,
		|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ТипУстройства КАК ТипУстройства,
		|	гхб_ЛимитНаМобильнуюСвязьЛимиты.НомерЛицевогоСчета КАК НомерЛицевогоСчета
		|ИЗ
		|	Документ.гхб_ЛимитНаМобильнуюСвязь.Лимиты КАК гхб_ЛимитНаМобильнуюСвязьЛимиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_НомераТелефонов КАК гхб_НомераТелефонов
		|		ПО гхб_ЛимитНаМобильнуюСвязьЛимиты.НомерТелефона = гхб_НомераТелефонов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_ФизическиеЛицаБазХолдинга КАК гхб_ФизическиеЛицаБазХолдингаФизЛицо
		|		ПО гхб_ЛимитНаМобильнуюСвязьЛимиты.ФизЛицоБазыХолдинга = гхб_ФизическиеЛицаБазХолдингаФизЛицо.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_ЦеновыеПакетыМобильнойСвязи КАК гхб_ЦеновыеПакетыМобильнойСвязи
		|		ПО гхб_ЛимитНаМобильнуюСвязьЛимиты.ЦеновойПакет = гхб_ЦеновыеПакетыМобильнойСвязи.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_ФизическиеЛицаБазХолдинга КАК гхб_ФизическиеЛицаБазХолдингаОтветственный
		|		ПО гхб_ЛимитНаМобильнуюСвязьЛимиты.ОтветственныйБазыХолдинга = гхб_ФизическиеЛицаБазХолдингаОтветственный.Ссылка
		|ГДЕ
		|	гхб_ЛимитНаМобильнуюСвязьЛимиты.Ссылка = &ЛимитНаМобильнуюСвязь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	гхб_НомераТелефонов.Ссылка КАК СсылкаТелефон,
		|	гхб_НомераТелефонов.Наименование КАК НаименованиеТелефон,
		|	гхб_ДополнительныеУслугиДляНомеровТелефонов.Ссылка КАК СсылкаДопУслуга,
		|	гхб_ДополнительныеУслугиДляНомеровТелефонов.Наименование КАК НаименованиеДопУслуга,
		|	гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.Сумма КАК Сумма,
		|	гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.Ссылка.БазаХолдинга КАК БазаХолдинга
		|ПОМЕСТИТЬ ВТДопУслуги
		|ИЗ
		|	Документ.гхб_ЛимитНаМобильнуюСвязь.ДополнительныеУслуги КАК гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_НомераТелефонов КАК гхб_НомераТелефонов
		|		ПО гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.НомерТелефона = гхб_НомераТелефонов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_ДополнительныеУслугиДляНомеровТелефонов КАК гхб_ДополнительныеУслугиДляНомеровТелефонов
		|		ПО гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.Услуга = гхб_ДополнительныеУслугиДляНомеровТелефонов.Ссылка
		|ГДЕ
		|	гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.Ссылка = &ЛимитНаМобильнуюСвязь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НЕ ВТДопУслуги.СсылкаТелефон ЕСТЬ NULL
		|			ТОГДА ВТДопУслуги.СсылкаТелефон
		|		ИНАЧЕ гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.НомерТелефона
		|	КОНЕЦ КАК СсылкаТелефон,
		|	ВЫБОР
		|		КОГДА НЕ ВТДопУслуги.СсылкаТелефон ЕСТЬ NULL
		|			ТОГДА ВТДопУслуги.НаименованиеТелефон
		|		ИНАЧЕ гхб_НомераТелефонов.Наименование
		|	КОНЕЦ КАК НаименованиеТелефон,
		|	ВЫБОР
		|		КОГДА НЕ ВТДопУслуги.СсылкаДопУслуга ЕСТЬ NULL
		|			ТОГДА ВТДопУслуги.СсылкаДопУслуга
		|		ИНАЧЕ гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.Услуга
		|	КОНЕЦ КАК СсылкаДопУслуга,
		|	ВЫБОР
		|		КОГДА НЕ ВТДопУслуги.СсылкаДопУслуга ЕСТЬ NULL
		|			ТОГДА ВТДопУслуги.НаименованиеДопУслуга
		|		ИНАЧЕ гхб_ДополнительныеУслугиДляНомеровТелефонов.Наименование
		|	КОНЕЦ КАК НаименованиеДопУслуга,
		|	ВЫБОР
		|		КОГДА НЕ ВТДопУслуги.Сумма ЕСТЬ NULL
		|			ТОГДА ВТДопУслуги.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма
		|ИЗ
		|	ВТДопУслуги КАК ВТДопУслуги
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.гхб_СоответствиеНомеровТелефоновДопУслугам.СрезПоследних(
		|				&ДатаНачалаДействия,
		|				БазаХолдинга = &БазаХолдинга
		|					И НомерТелефона В
		|						(ВЫБРАТЬ
		|							ВТДопУслуги.СсылкаТелефон
		|						ИЗ
		|							ВТДопУслуги)
		|					И Регистратор <> &ЛимитНаМобильнуюСвязь) КАК гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_НомераТелефонов КАК гхб_НомераТелефонов
		|			ПО гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.НомерТелефона = гхб_НомераТелефонов.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гхб_ДополнительныеУслугиДляНомеровТелефонов КАК гхб_ДополнительныеУслугиДляНомеровТелефонов
		|			ПО гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.Услуга = гхб_ДополнительныеУслугиДляНомеровТелефонов.Ссылка
		|		ПО ВТДопУслуги.БазаХолдинга = гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.БазаХолдинга
		|			И ВТДопУслуги.СсылкаТелефон = гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.НомерТелефона
		|			И ВТДопУслуги.СсылкаДопУслуга = гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.Услуга
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ВТДопУслуги.Сумма ЕСТЬ NULL
		|				ТОГДА гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.Сумма <> 0
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДопУслуги";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция СформироватьТЗЛимиты(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	гхб_ЛимитНаМобильнуюСвязь.ДатаНачалаДействия КАК Период,
	|	гхб_ЛимитНаМобильнуюСвязь.БазаХолдинга КАК БазаХолдинга,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.НомерТелефона КАК НомерТелефона,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ФизЛицоБазыХолдинга КАК ФизЛицоБазыХолдинга,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ЛимитВнутренний КАК ЛимитВнутренний,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ЛимитМеждународнойСвязи КАК ЛимитМеждународнойСвязи,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.Использование КАК Использование,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.БезлимитныйВнутренний КАК БезлимитныйВнутренний,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.БезлимитнаяМеждународнаяСвязь КАК БезлимитнаяМеждународнаяСвязь,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ЦеновойПакет КАК ЦеновойПакет,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ОтветственныйБазыХолдинга КАК ОтветственныйБазыХолдинга,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.ТипУстройства КАК ТипУстройства,
	|	гхб_ЛимитНаМобильнуюСвязьЛимиты.НомерЛицевогоСчета КАК НомерЛицевогоСчета
	|ИЗ
	|	Документ.гхб_ЛимитНаМобильнуюСвязь.Лимиты КАК гхб_ЛимитНаМобильнуюСвязьЛимиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гхб_ЛимитНаМобильнуюСвязь КАК гхб_ЛимитНаМобильнуюСвязь
	|		ПО гхб_ЛимитНаМобильнуюСвязьЛимиты.Ссылка = гхб_ЛимитНаМобильнуюСвязь.Ссылка
	|ГДЕ
	|	гхб_ЛимитНаМобильнуюСвязь.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции

// Если доп. услуга была удалена в текущем документе, то ей устанавливается сумма 0 при проведении
Функция СформироватьТЗДополнительныеУслуги(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	гхб_ЛимитНаМобильнуюСвязь.БазаХолдинга КАК БазаХолдинга,
	|	гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.НомерТелефона КАК НомерТелефона,
	|	гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.Услуга КАК Услуга,
	|	гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТДанныеИзДокумента
	|ИЗ
	|	Документ.гхб_ЛимитНаМобильнуюСвязь.ДополнительныеУслуги КАК гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гхб_ЛимитНаМобильнуюСвязь КАК гхб_ЛимитНаМобильнуюСвязь
	|		ПО гхб_ЛимитНаМобильнуюСвязьДополнительныеУслуги.Ссылка = гхб_ЛимитНаМобильнуюСвязь.Ссылка
	|ГДЕ
	|	гхб_ЛимитНаМобильнуюСвязь.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ДатаНачалаДействия КАК Период,
	|	ВЫБОР
	|		КОГДА НЕ ВТДанныеИзДокумента.БазаХолдинга ЕСТЬ NULL
	|			ТОГДА ВТДанныеИзДокумента.БазаХолдинга
	|		ИНАЧЕ гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.БазаХолдинга
	|	КОНЕЦ КАК БазаХолдинга,
	|	ВЫБОР
	|		КОГДА НЕ ВТДанныеИзДокумента.НомерТелефона ЕСТЬ NULL
	|			ТОГДА ВТДанныеИзДокумента.НомерТелефона
	|		ИНАЧЕ гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.НомерТелефона
	|	КОНЕЦ КАК НомерТелефона,
	|	ВЫБОР
	|		КОГДА НЕ ВТДанныеИзДокумента.Услуга ЕСТЬ NULL
	|			ТОГДА ВТДанныеИзДокумента.Услуга
	|		ИНАЧЕ гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.Услуга
	|	КОНЕЦ КАК Услуга,
	|	ВЫБОР
	|		КОГДА НЕ ВТДанныеИзДокумента.Сумма ЕСТЬ NULL
	|			ТОГДА ВТДанныеИзДокумента.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	ВТДанныеИзДокумента КАК ВТДанныеИзДокумента
	|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.гхб_СоответствиеНомеровТелефоновДопУслугам.СрезПоследних(
	|				&ДатаНачалаДействия,
	|				БазаХолдинга = &БазаХолдинга
	|					И НомерТелефона В
	|						(ВЫБРАТЬ
	|							ВТДанныеИзДокумента.НомерТелефона
	|						ИЗ
	|							ВТДанныеИзДокумента)
	|					И Регистратор <> &Ссылка) КАК гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних
	|		ПО ВТДанныеИзДокумента.БазаХолдинга = гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.БазаХолдинга
	|			И ВТДанныеИзДокумента.НомерТелефона = гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.НомерТелефона
	|			И ВТДанныеИзДокумента.Услуга = гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.Услуга
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТДанныеИзДокумента.Сумма ЕСТЬ NULL
	|				ТОГДА гхб_СоответствиеНомеровТелефоновДопУслугамСрезПоследних.Сумма <> 0
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДанныеИзДокумента";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	СтруктураЗначений = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "ДатаНачалаДействия, БазаХолдинга");
	Запрос.УстановитьПараметр("ДатаНачалаДействия", СтруктураЗначений.ДатаНачалаДействия);
	Запрос.УстановитьПараметр("БазаХолдинга", СтруктураЗначений.БазаХолдинга);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить();

КонецФункции

Процедура СформироватьДвиженияПоТЗ(Движения, ТЗПроведения, НазваниеРегистра)
	
	Движения[НазваниеРегистра].Записывать = Истина;
	Движения[НазваниеРегистра].Очистить();
	
	Для Каждого СтрокаПроведения Из ТЗПроведения Цикл
		
		Движение = Движения[НазваниеРегистра].Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаПроведения);	
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли