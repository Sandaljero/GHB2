
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если НЕ РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента) Тогда
		
		ДвиженияПоПомещения();
		ДвиженияПоЮридическиеЛицаВПомещениях();
		
		РегистрыСведений.гхб_СобытияОбъектовБД.ЗаписатьДанныеСобытияОбъекта(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
    КонецЕсли;
	
	Если (РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) 
	   И РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента) Тогда
	   
	   	Отказ = Истина;
		
	Иначе
		
		УдалитьСтрокиТЧСНеправильнымиПомещениями(ЮридическиеЛицаВПомещениях);
		
		Для Каждого Стр Из Помещения Цикл
			Стр.ЮридическиеЛицаВПомещении = "";
			Стр.ДивизионыВПомещении = "";
			Стр.ДепартаментыВПомещении = "";
		КонецЦикла;
		
		ЗаполнитьСокращенныеДанные(ЮридическиеЛицаВПомещениях, "ЮридическиеЛицаВПомещении", "ЮридическоеЛицо");
		ЗаполнитьСокращенныеДанные(ЮридическиеЛицаВПомещениях, "ДивизионыВПомещении", "Дивизион");
		ЗаполнитьСокращенныеДанные(ЮридическиеЛицаВПомещениях, "ДепартаментыВПомещении", "Департамент");
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(гхб_Ответственный) Тогда
		гхб_Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	гхб_Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДвиженияПоПомещения()
	
	Для Каждого Стр Из Помещения Цикл
		
		_Строка = Движения.гхб_РаспределениеДоступовВПомещения.Добавить();
		_Строка.Период = Дата;
		_Строка.Регистратор = Ссылка;
		_Строка.Помещение = Стр.Помещение;
		_Строка.ДепартаментРаспорядитель = Стр.ДепартаментРаспорядитель;
		_Строка.ДепартаментыВПомещении = Стр.ДепартаментыВПомещении;
		_Строка.ДивизионРаспорядитель = Стр.ДивизионРаспорядитель;
		_Строка.ДивизионыВПомещении = Стр.ДивизионыВПомещении;
		_Строка.ЮридическоеЛицоРаспорядитель = Стр.ЮридическоеЛицоРаспорядитель;
		_Строка.ЮридическоеЛицоСобственник = Стр.ЮридическоеЛицоСобственник;
		_Строка.ЮридическиеЛицаВПомещении = Стр.ЮридическиеЛицаВПомещении;
		
	КонецЦикла;
	
	Движения.гхб_РаспределениеДоступовВПомещения.Записать();
	
КонецПроцедуры

Процедура ДвиженияПоЮридическиеЛицаВПомещениях()
	
	Для Каждого Стр Из ЮридическиеЛицаВПомещениях Цикл
		
		Если (Стр.СуществующийДоступ И Стр.Закрыть) ИЛИ (НЕ Стр.СуществующийДоступ И НЕ Стр.Закрыть) Тогда
			
			_Строка = Движения.гхб_РаспределениеДоступовВПомещенияЮридическиеЛица.Добавить();
			_Строка.Период = Дата;
			_Строка.Регистратор = Ссылка;
			_Строка.Помещение = Стр.Помещение;
			_Строка.ЮридическоеЛицо = Стр.ЮридическоеЛицо;
			_Строка.Дивизион = Стр.Дивизион;
			_Строка.Департамент = Стр.Департамент;
			_Строка.Площадь = Стр.Площадь;
			_Строка.УточняющаяИнформация = Стр.УточняющаяИнформация;
			_Строка.Закрыть = Стр.Закрыть;
			
		КонецЕсли;
			
	КонецЦикла;
	
	Движения.гхб_РаспределениеДоступовВПомещенияЮридическиеЛица.Записать();
	
КонецПроцедуры

Процедура ЗаполнитьСокращенныеДанные(_ТЧ, _ИмяПоляПриемника, _ИмяПоляИсточника)
	
	Для Каждого Стр Из _ТЧ Цикл
		_НС = Помещения.Найти(Стр.Помещение, "Помещение");
		
		Если НЕ (_НС = Неопределено) Тогда
			_НС[_ИмяПоляПриемника] = _НС[_ИмяПоляПриемника] + ?(ЗначениеЗаполнено(_НС[_ИмяПоляПриемника]), "; ", "") +
				СокрЛП(Стр[_ИмяПоляИсточника]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьСтрокиТЧСНеправильнымиПомещениями(_ТЧ)
	
	ф = _ТЧ.Количество() - 1;
	
	Пока ф >= 0 Цикл
		
		Если Помещения.Найти(_ТЧ.Получить(ф).Помещение, "Помещение") = Неопределено Тогда
			_ТЧ.Удалить(ф);
		КонецЕсли;
		
		ф = ф - 1;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
