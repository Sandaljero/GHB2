
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДокументБылПроведен(_Ссылка)
	
	Возврат РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(_Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента);
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	ЭтотОбъект.ТолькоПросмотр = ДокументБылПроведен(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗакрытьПоРодителюВИстина(_ТекущиеДанные)
	
	Если НЕ (_ТекущиеДанные = Неопределено) Тогда
	
		Если _ТекущиеДанные.Закрыть И НЕ _ТекущиеДанные.СуществующийДоступ Тогда
		
			_ТекущиеДанные.Закрыть = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаВПомещенияхЗакрытьПриИзменении(Элемент)
	
	_ТД = Элементы.ЮридическиеЛицаВПомещениях.ТекущиеДанные;
	УстановитьЗакрытьПоРодителюВИстина(_ТД);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаВПомещенияхПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = (Элементы.Помещения.ТекущиеДанные = Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаВПомещенияхПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		ТД = Элементы.ЮридическиеЛицаВПомещениях.ТекущиеДанные;
		ТД.Помещение = Элементы.Помещения.ТекущиеДанные.Помещение;
		ТД.СуществующийДоступ = Ложь;
		ТД.Закрыть = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПомещенияПомещениеПриИзменении(Элемент)
	
	УстановитьОтборПоПомещению(Элементы.Помещения.ТекущиеДанные.Помещение);
	ЗаполнитьДанныеПоПомещению(Элементы.Помещения.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоПомещению(_Помещение)
	
	СтруктураПоиска = Новый Структура("Помещение", _Помещение);
	
	Элементы.ЮридическиеЛицаВПомещениях.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураПоиска);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПоПомещению(_ТД)

	ЗаполнитьЗначенияСвойств(_ТД, ВернутьДанныеПоПомещению(_ТД.Помещение), , "Помещение");
	
	_СтруктураПоиска = Новый Структура("Помещение", _ТД.Помещение);
	
	Если Объект.ЮридическиеЛицаВПомещениях.НайтиСтроки(_СтруктураПоиска).Количество() = 0 Тогда
		_Массив = ВернутьДанныеЮридическиеЛицаВПомещениях(_ТД.Помещение);
		
		Для Каждого _Структура Из _Массив Цикл
			НС = Объект.ЮридическиеЛицаВПомещениях.Добавить();
			НС.Помещение = _ТД.Помещение;
			НС.ЮридическоеЛицо = _Структура.ЮридическоеЛицо;
			НС.Дивизион = _Структура.Дивизион;
			НС.Департамент = _Структура.Департамент;
			НС.Площадь = _Структура.Площадь;
			НС.УточняющаяИнформация = _Структура.УточняющаяИнформация;
			НС.СуществующийДоступ = Истина;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьДанныеПоПомещению(_Помещение)
	
	Возврат РегистрыСведений.гхб_РаспределениеДоступовВПомещения.ВернутьДанныеПоПомещению(_Помещение);
	
КонецФункции

&НаСервереБезКонтекста
Функция ВернутьДанныеЮридическиеЛицаВПомещениях(_Помещение)
	
	Возврат РегистрыСведений.гхб_РаспределениеДоступовВПомещенияЮридическиеЛица.ВернутьДанныеПоПомещению(_Помещение);
	
КонецФункции

&НаКлиенте
Процедура ПомещенияПриАктивизацииСтроки(Элемент)
	
	_ТД = Элементы.Помещения.ТекущиеДанные;
	
	УстановитьОтборПоПомещению(?(_ТД = Неопределено, ПредопределенноеЗначение("Справочник.гхб_Помещения.ПустаяСсылка"), _ТД.Помещение));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаВПомещенияхПередУдалением(Элемент, Отказ)
	
	_ТекущиеДанные = Элементы.ЮридическиеЛицаВПомещениях.ТекущиеДанные;
	
	Если НЕ (_ТекущиеДанные = Неопределено) И _ТекущиеДанные.СуществующийДоступ Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЮридическиеЛицаВПомещенияхПриАктивизацииСтроки(Элемент)
	
	_ТекущиеДанные = Элементы.ЮридическиеЛицаВПомещениях.ТекущиеДанные;
	
	Если НЕ (_ТекущиеДанные = Неопределено) Тогда
		Элементы.ЮридическиеЛицаВПомещенияхЮридическоеЛицо.ТолькоПросмотр = _ТекущиеДанные.СуществующийДоступ;
		Элементы.ЮридическиеЛицаВПомещенияхПлощадь.ТолькоПросмотр = _ТекущиеДанные.СуществующийДоступ;
		Элементы.ЮридическиеЛицаВПомещенияхУточняющаяИнформация.ТолькоПросмотр = _ТекущиеДанные.СуществующийДоступ;
	КонецЕсли;
	
КонецПроцедуры

