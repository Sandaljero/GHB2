
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(гхб_Ответственный) Тогда
		гхб_Ответственный = Пользователи.ТекущийПользователь();
		Помещение = Справочники.гхб_Помещения.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	гхб_Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
    КонецЕсли;
	
	СотрудникиКраткийСостав = "";
	
	Для Каждого Стр Из Сотрудники Цикл
		СотрудникиКраткийСостав = СотрудникиКраткийСостав + ?(ЗначениеЗаполнено(СотрудникиКраткийСостав), "; ", "") +
			ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СокрЛП(Стр.Сотрудник)) + ?(Стр.Закрыть, " (з)", "");
	КонецЦикла;
	
	Если (РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) 
	   И РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента) Тогда
	   
	   	Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если НЕ РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента) Тогда
		
		ДвиженияПоЗакреплениюСотрудников();
			
		РегистрыСведений.гхб_СобытияОбъектовБД.ЗаписатьДанныеСобытияОбъекта(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ТипЗакрепления = 0 Тогда // за помещением
		ПроверяемыеРеквизиты.Добавить("Помещение");
	ИначеЕсли ТипЗакрепления = 1 Тогда // за сотрудником
		ПроверяемыеРеквизиты.Добавить("Сотрудник");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДвиженияПоЗакреплениюСотрудников()
	
	Для Каждого Стр Из Сотрудники Цикл
		
		Если (Стр.СуществующийДоступ И (Стр.Закрыть ИЛИ НЕ (Стр.Плавающее = Стр.ПлавающееСтарое) ИЛИ НЕ (Стр.РежимРаботы = Стр.РежимРаботыСтарый))) 
		 ИЛИ (НЕ Стр.СуществующийДоступ И НЕ Стр.Закрыть) Тогда 
		 
		 	Если ТипЗнч(Стр.Помещение) = Тип("СправочникСсылка.гхб_ГруппыПомещений") Тогда
		 
				_Строка = Движения.гхб_ЗакреплениеСотрудниковЗаГруппойПомещений.Добавить();
				_Строка.ГруппаПомещений = Стр.Помещение;
				
			Иначе 
				
				_Строка = Движения.гхб_ЗакреплениеСотрудниковЗаПомещениями.Добавить();
				_Строка.Помещение = Стр.Помещение;
				
			КонецЕсли;
			
			_Строка.Период = Дата;
			_Строка.Регистратор = Ссылка;
			_Строка.Сотрудник = Стр.Сотрудник;
			_Строка.РежимРаботы = Стр.РежимРаботы;
			_Строка.Плавающее = Стр.Плавающее;
			_Строка.Закрыть = Стр.Закрыть;
		
		КонецЕсли;
		
	КонецЦикла;
		
	Если НЕ (Движения.гхб_ЗакреплениеСотрудниковЗаГруппойПомещений.Количество() = 0) Тогда
		Движения.гхб_ЗакреплениеСотрудниковЗаГруппойПомещений.Записать();
	КонецЕсли;
	
	Если НЕ (Движения.гхб_ЗакреплениеСотрудниковЗаПомещениями.Количество() = 0) Тогда
		Движения.гхб_ЗакреплениеСотрудниковЗаПомещениями.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли