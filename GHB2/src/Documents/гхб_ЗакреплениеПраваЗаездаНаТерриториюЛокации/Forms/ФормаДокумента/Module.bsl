
&НаКлиенте
Процедура ДанныеФИОПриИзменении(Элемент)
	
	ТД = Элементы.Данные.ТекущиеДанные;
	
	Если НЕ (ТД = Неопределено) И ЗначениеЗаполнено(ТД.ФИО) Тогда
		ТД.УровеньДолжности = ПолучитьУровеньДолжностиПоОсновномуМестуРаботы(ТД.ФИО);
		Элементы.ДанныеАвтомобиль.СписокВыбора.ЗагрузитьЗначения(ВернутьАвтомобилиДляСотрудника(ТД.ФИО));
	Иначе
		Элементы.ДанныеАвтомобиль.СписокВыбора.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьАвтомобилиДляСотрудника(_ФЛ)

	Возврат РегистрыСведений.гхб_ПраваЗаездаНаТерриториюЛокации.ВернутьДоступныеАвтомобилиДляСотрудника(_ФЛ)
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьУровеньДолжностиПоОсновномуМестуРаботы(_ФЛ)
	
	_УровеньДолжности = Перечисления.гхб_УровниДолжностей.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(_ФЛ) Тогда
		
		ЗапросКадры = Новый Запрос();
		ЗапросКадры.Текст = "ВЫБРАТЬ
		                    |	гхб_ШтатноеРасписаниеБазХолдингаСрезПоследних.УровеньДолжности КАК УровеньДолжности
		                    |ИЗ
		                    |	РегистрСведений.гхб_РаботникиОрганизацийБазХолдинга.СрезПоследних(, Сотрудник.ФизЛицоБазыХолдинга.ФизЛицоТекущейБазы = &ФизЛицоТекущейБазы) КАК гхб_РаботникиОрганизацийБазХолдингаСрезПоследних
		                    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.гхб_ШтатноеРасписаниеБазХолдинга.СрезПоследних КАК гхб_ШтатноеРасписаниеБазХолдингаСрезПоследних
		                    |		ПО гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.БазаХолдинга = гхб_ШтатноеРасписаниеБазХолдингаСрезПоследних.БазаХолдинга
		                    |			И гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.Организация = гхб_ШтатноеРасписаниеБазХолдингаСрезПоследних.Организация
		                    |			И гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.ПодразделениеОрганизации = гхб_ШтатноеРасписаниеБазХолдингаСрезПоследних.ПодразделениеОрганизации
		                    |			И гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.ПозицияШтатногоРасписания = гхб_ШтатноеРасписаниеБазХолдингаСрезПоследних.Позиция
		                    |ГДЕ
		                    |	гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.гхб_ПричиныИзмененияСостояния.Увольнение)
		                    |	И гхб_РаботникиОрганизацийБазХолдингаСрезПоследних.Сотрудник.ВидЗанятости В(&ВидЗанятости)";
		
		_СписокВидовЗанятости = Новый СписокЗначений();
		_СписокВидовЗанятости.Добавить(Перечисления.гхб_ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы);
		_СписокВидовЗанятости.Добавить(Перечисления.гхб_ВидыЗанятостиВОрганизации.Совместительство);

		ЗапросКадры.УстановитьПараметр("ВидЗанятости", _СписокВидовЗанятости);
		ЗапросКадры.УстановитьПараметр("ФизЛицоТекущейБазы", _ФЛ);
		
		Выборка = ЗапросКадры.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда 
			_УровеньДолжности = Выборка.УровеньДолжности;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат _УровеньДолжности;
	
КонецФункции

&НаКлиенте
Процедура ДанныеАвтомобильПриИзменении(Элемент)
	
	ТД = Элементы.Данные.ТекущиеДанные;
	
	Если НЕ (ТД = Неопределено) И ЗначениеЗаполнено(ТД.Автомобиль) Тогда
		ТД.ТипАвтомобиля = ВернутьТипАвтомобиля(ТД.Автомобиль);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьТипАвтомобиля(_Автомобиль)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(_Автомобиль, "ТипАвтомобиля")
	
КонецФункции

&НаКлиенте
Процедура ДанныеТипАвтомобиляПриИзменении(Элемент)
	
	ТД = Элементы.Данные.ТекущиеДанные;
	
	Если НЕ (ТД = Неопределено) Тогда
		Если ЗначениеЗаполнено(ТД.Автомобиль) И ЗначениеЗаполнено(ТД.ТипАвтомобиля) И НЕ (ТД.ТипАвтомобиля = ВернутьТипАвтомобиля(ТД.Автомобиль)) Тогда
			ТД.Автомобиль = ПредопределенноеЗначение("Справочник.гхб_Автомобили.ПустаяСсылка");
		КонецЕсли;
		
		УстановитьПараметрыВыбораДляАвтомобиля(ТД.ТипАвтомобиля);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыВыбораДляАвтомобиля(_ТипАвтомобиля)
	
	НовыйПараметр = Новый ПараметрВыбора("ФильтрПоТипуАвтомобиля", _ТипАвтомобиля);
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметр);
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	
	Элементы.ДанныеАвтомобиль.ПараметрыВыбора = НовыеПараметры;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	_Массив = Новый Массив();
	
	Если РольДоступна(Метаданные.Роли.ПолныеПрава) Тогда
		
		_Массив = РегистрыСведений.гхб_ИспользованиеЛокацийДляЗакрепленияПарковочныхМест.ВернутьДоступныеЛокацииДляПарковок();
		
	Иначе
		
		_МассивЛокацийДляПарковок = РегистрыСведений.гхб_ИспользованиеЛокацийДляЗакрепленияПарковочныхМест.ВернутьДоступныеЛокацииДляПарковок();

		_ФЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСеанса.ТекущийПользователь, "ФизическоеЛицо");
		
		_МассивДоступныхЛокаций = РегистрыСведений.гхб_НастройкиВозможностейЗакрепленияПарковочныхМест.ВернутьДоступныеЛокацииСотрудникаДляРедактированияДокумента(_ФЛ);
		
		Для Каждого Стр Из _МассивДоступныхЛокаций Цикл
			Если НЕ (_МассивЛокацийДляПарковок.Найти(Стр) = Неопределено) Тогда
				_Массив.Добавить(Стр);
			КонецЕсли;
		КонецЦикла;
		
		_МассивДоступныхЛокаций = РегистрыСведений.гхб_РуководителиЛокаций.ВернутьЛокацииПоРуководителю(_ФЛ);
		
		Для Каждого Стр Из _МассивДоступныхЛокаций Цикл
			Если (_Массив.Найти(Стр) = Неопределено) И НЕ (_МассивЛокацийДляПарковок.Найти(Стр) = Неопределено) Тогда
				_Массив.Добавить(Стр);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И (_Массив.Найти(Объект.Локация) = Неопределено) Тогда
		ЭтотОбъект.ТолькоПросмотр = Истина;
	Иначе 	
		Элементы.Локация.СписокВыбора.ЗагрузитьЗначения(_Массив);
		Элементы.Локация.СписокВыбора.СортироватьПоЗначению();
		Элементы.Локация.РежимВыбораИзСписка = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеПриАктивизацииСтроки(Элемент)
	
	ТД = Элементы.Данные.ТекущиеДанные;
	
	Если НЕ (ТД = Неопределено) Тогда
		
		УстановитьПараметрыВыбораДляАвтомобиля(ТД.ТипАвтомобиля);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДанныеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
	
		ТД = Элементы.Данные.ТекущиеДанные;
		
		Если НЕ ЗначениеЗаполнено(ТД.ТипОперации) Тогда
			ТД.ТипОперации = ПредопределенноеЗначение("Перечисление.гхб_ТипОперацииЗакреплениеОткреплениеПарковочногоМеста.Закрепление");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
