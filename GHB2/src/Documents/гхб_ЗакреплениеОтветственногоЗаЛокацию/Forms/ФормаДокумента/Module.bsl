
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	_ФЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСеанса.ТекущийПользователь, "ФизическоеЛицо");
	_ПараметрРуководитель = ?(РольДоступна(Метаданные.Роли.ПолныеПрава), Неопределено, _ФЛ);
	_Массив = РегистрыСведений.гхб_РуководителиЛокаций.ВернутьЛокацииПоРуководителю(_ПараметрРуководитель);
	
	Для Каждого _Локация Из _Массив Цикл
		Элементы.Локация.СписокВыбора.Добавить(_Локация, СокрЛП(_Локация));
	КонецЦикла;
	
	Элементы.Локация.СписокВыбора.СортироватьПоПредставлению();
	
	Если Параметры.Свойство("Локация") Тогда
		Объект.Локация = Параметры.Локация;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Локация) Тогда
		ПроверитьСуществованиеОтветственного();
	КонецЕсли;	
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСуществованиеОтветственного()
	
	Если ЗначениеЗаполнено(Объект.ОтветственныйЗаЛокациюСуществующий) Тогда
		Объект.ОтветственныйЗаЛокациюСуществующий = ПредопределенноеЗначение("Справочник.гхб_Помещения.ПустаяСсылка");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Локация) Тогда
		Объект.ОтветственныйЗаЛокациюСуществующий = ВернутьОтветственногоЗаЛокацию(Объект.Локация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	ЭтотОбъект.ТолькоПросмотр = ДокументБылПроведен(Объект.Ссылка);
	
	Элементы.ОтветственныйЗаЛокациюСуществующий.Видимость = ЗначениеЗаполнено(Объект.ОтветственныйЗаЛокациюСуществующий);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДокументБылПроведен(_Ссылка)
	
	Возврат РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(_Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента);
	
КонецФункции

&НаКлиенте
Процедура ЛокацияПриИзменении(Элемент)
	
	ПроверитьСуществованиеОтветственного();
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьОтветственногоЗаЛокацию(_Локация)
	
	Возврат РегистрыСведений.гхб_ОтветственныеЗаЛокации.ВернутьОтветственногоЗаЛокацию(_Локация);
	
КонецФункции