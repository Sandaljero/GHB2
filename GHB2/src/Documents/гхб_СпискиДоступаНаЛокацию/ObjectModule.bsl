
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(гхб_Ответственный) Тогда
		гхб_Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	гхб_Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
	КонецЕсли;
	 
	Если (РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) Тогда
		
		Отказ = (ДатаОкончания < НачалоДня(ТекущаяДатаСеанса()));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ПеренесеноИзБазыРесурсов Тогда
		
		Для Каждого Стр Из ДанныеПеренесенныеИзБазыРесурсов Цикл
			
			Если НЕ ЗначениеЗаполнено(Стр.Сотрудник) Тогда
				Продолжить;
			КонецЕсли;
			
			_ТмпДата = Стр.ДатаНачала;
			
			Пока _ТмпДата <= Стр.ДатаОкончания Цикл
				
				_Движение = Движения.гхб_СпискиДоступаНаЛокацию.Добавить();
				_Движение.Дата = _ТмпДата;
				_Движение.Сотрудник = Стр.Сотрудник;
				_Движение.Локация = Стр.Локация;
				_Движение.ИдентификаторДокументаБазыРесурсов = ИдентификаторДокументаБазыРесурсов;
				
				_ТмпДата = гхб_ОбщегоНазначенияКлиентСервер.ДобавитьДеньКДате(_ТмпДата);
				
			КонецЦикла;
			
		КонецЦикла;
		
	Иначе
	
		Для Каждого Стр Из Данные Цикл
			
			Если НЕ ЗначениеЗаполнено(Стр.Сотрудник) Тогда
				Продолжить;
			КонецЕсли;
			
			Для ф = 1 По 7 Цикл
				
				Если ЗначениеЗаполнено(Стр["Дата" + ф]) Тогда
					
					_Движение = Движения.гхб_СпискиДоступаНаЛокацию.Добавить();
					_Движение.Дата = гхб_ОбщегоНазначенияКлиентСервер.ДобавитьДеньКДате(ДатаНачала, ф - 1);
					_Движение.Сотрудник = Стр.Сотрудник;
					_Движение.Локация = Стр.Локация;
					
				КонецЕсли;
				
			КонецЦикла;
				
		КонецЦикла;
		
	КонецЕсли;
	
	Движения.гхб_СпискиДоступаНаЛокацию.Записать();
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Префикс = ?(ПеренесеноИзБазыРесурсов, "МР", "СД");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли