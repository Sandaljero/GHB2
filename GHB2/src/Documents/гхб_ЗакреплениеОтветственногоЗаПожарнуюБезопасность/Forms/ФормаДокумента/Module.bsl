
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	_ФЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСеанса.ТекущийПользователь, "ФизическоеЛицо");
	
	_Администратор = РольДоступна(Метаданные.Роли.ПолныеПрава) 
		ИЛИ (_ФЛ = Справочники.гхб_СлужебныеЗначения.РуководительОтделаОтиПБ.Значение);
		
	Если НЕ _Администратор Тогда
		
		_Массив = РегистрыСведений.гхб_СотрудникиСАдминистративнымиПравамиПБ.ВернутьСотрудниковСАдминистративнымиПравамиПожарнаяБезопасность();
		
		_Администратор = НЕ (_Массив.Найти(_ФЛ) = Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	ЭтотОбъект.ТолькоПросмотр = НЕ _Администратор ИЛИ ДокументБылПроведен(Объект.Ссылка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДокументБылПроведен(_Ссылка)
	
	Возврат РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(_Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	
	_ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если НЕ (_ТекущиеДанные = Неопределено) И _ТекущиеДанные.СуществующийДоступ Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиЗакрытьПриИзменении(Элемент)
	
	_ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если НЕ (_ТекущиеДанные = Неопределено) И _ТекущиеДанные.Закрыть И НЕ _ТекущиеДанные.СуществующийДоступ Тогда
		
		_ТекущиеДанные.Закрыть = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеПриИзменении(Элемент)
	
	ЗагрузитьАктуальныхСотрудников(Объект.Помещение)
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьАктуальныхСотрудников(_Помещение)
	
	Объект.Сотрудники.Очистить();
	
	_МассивСотрудников = ВернутьАктуальныхСотрудниковДляПомещения(_Помещение);
	
	Для Каждого _Сотрудник Из _МассивСотрудников Цикл
		
		НС = Объект.Сотрудники.Добавить();
		НС.Сотрудник = _Сотрудник;
		НС.СуществующийДоступ = Истина;
			
	КонецЦикла;
	
	Объект.Сотрудники.Сортировать("СуществующийДоступ УБЫВ, Сотрудник ВОЗР");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьАктуальныхСотрудниковДляПомещения(_Помещение)
	
	Возврат РегистрыСведений.гхб_ОтветственныеЗаПожарнуюБезопасность.ВернутьАктуальныхСотрудниковДляПомещения(_Помещение);
	
КонецФункции

&НаКлиенте
Процедура СотрудникиПриАктивизацииСтроки(Элемент)
	
	_ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если НЕ (_ТекущиеДанные = Неопределено) Тогда
		Элементы.СотрудникиСотрудник.ТолькоПросмотр = _ТекущиеДанные.СуществующийДоступ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
	
		ТД = Элементы.Сотрудники.ТекущиеДанные;
		ТД.СуществующийДоступ = Ложь;
		ТД.Закрыть = Ложь;

	КонецЕсли;
	
КонецПроцедуры
