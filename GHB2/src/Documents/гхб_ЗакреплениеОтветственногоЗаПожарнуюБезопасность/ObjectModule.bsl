
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(гхб_Ответственный) Тогда
		гхб_Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	гхб_Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если НЕ РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента) Тогда
		
		ДвиженияПоЗакреплениюОтветственныхЗаПожарнуюБезопасность();
		
		РегистрыСведений.гхб_СобытияОбъектовБД.ЗаписатьДанныеСобытияОбъекта(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента);
		
		ЗаполнитьОтветственныхЗаПожарнуюБезопасностьВДополнительно();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
         Возврат;
    КонецЕсли;
	
	СотрудникиКраткийСостав = "";
	
	Для Каждого Стр Из Сотрудники Цикл
		СотрудникиКраткийСостав = СотрудникиКраткийСостав + ?(ЗначениеЗаполнено(СотрудникиКраткийСостав), "; ", "") +
			ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СокрЛП(Стр.Сотрудник)) + ?(Стр.Закрыть, " (з)", "");
	КонецЦикла;
	
	Если (РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) 
	   И РегистрыСведений.гхб_СобытияОбъектовБД.СобытиеПоОбъектуБыло(Ссылка, Перечисления.гхб_ТипыСобытийОбъектовБД.ПервоеПроведениеДокумента) Тогда
	   
	   	Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДвиженияПоЗакреплениюОтветственныхЗаПожарнуюБезопасность()
	
	Для Каждого Стр Из Сотрудники Цикл
		
		Если (Стр.СуществующийДоступ И Стр.Закрыть) ИЛИ (НЕ Стр.СуществующийДоступ И НЕ Стр.Закрыть) Тогда
		
			_Строка = Движения.гхб_ОтветственныеЗаПожарнуюБезопасность.Добавить();
			_Строка.Период = Дата;
			_Строка.Регистратор = Ссылка;
			_Строка.Помещение = Помещение;
			_Строка.ОтветственныйЗаПожарнуюБезопасность = Стр.Сотрудник;
			_Строка.Закрыть = Стр.Закрыть;
		
		КонецЕсли;
		
	КонецЦикла;
		
	Движения.гхб_ОтветственныеЗаПожарнуюБезопасность.Записать();
	
КонецПроцедуры

Процедура ЗаполнитьОтветственныхЗаПожарнуюБезопасностьВДополнительно()
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних.Помещение КАК Помещение
	               |ПОМЕСТИТЬ _Помещения
	               |ИЗ
	               |	РегистрСведений.гхб_ОтветственныеЗаПожарнуюБезопасность.СрезПоследних КАК гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних
	               |ГДЕ
	               |	гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних.Регистратор = &Регистратор
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних.Помещение
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	Помещение
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних.Помещение КАК Помещение,
	               |	гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних.ОтветственныйЗаПожарнуюБезопасность КАК ОтветственныйЗаПожарнуюБезопасность,
	               |	гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних.Закрыть КАК Закрыть
	               |ИЗ
	               |	РегистрСведений.гхб_ОтветственныеЗаПожарнуюБезопасность.СрезПоследних КАК гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ _Помещения КАК _Помещения
	               |		ПО гхб_ОтветственныеЗаПожарнуюБезопасностьСрезПоследних.Помещение = _Помещения.Помещение
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Помещение,
	               |	ОтветственныйЗаПожарнуюБезопасность
	               |ИТОГИ ПО
	               |	Помещение
	               |АВТОУПОРЯДОЧИВАНИЕ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ _Помещения";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	ВыборкаПомещения = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПомещения.Следующий() Цикл
		
		_Стр = "";
		
		ВыборкаОтветственные = ВыборкаПомещения.Выбрать();
		
		Пока ВыборкаОтветственные.Следующий() Цикл
			Если НЕ ВыборкаОтветственные.Закрыть Тогда
				_Стр = _Стр + ?(ЗначениеЗаполнено(_Стр), "; ", "") + СокрЛП(ВыборкаОтветственные.ОтветственныйЗаПожарнуюБезопасность);
			КонецЕсли;
		КонецЦикла;
		
		МЗ = РегистрыСведений.гхб_ДополнительныеСвойстваПомещений.СоздатьМенеджерЗаписи();
		МЗ.Помещение = ВыборкаПомещения.Помещение;
		МЗ.Прочитать();
		
		МЗ.Помещение = ВыборкаПомещения.Помещение;
		МЗ.ОтветственныеЗаПожарнуюБезопасность = _Стр;
		МЗ.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли